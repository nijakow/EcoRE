(
    |
        sdl <- (std os files here / '..' / 'modules' / 'gfx' / 'sdl' / '_.eco') import
    |
    sdl Init.

    |
        win   <- sdl NewWindowWithTitle: 'Hello' X: 100 Y: 100 W: 800 H: 600,
        ren   <- win renderer,
        event <- sdl NewEvent
    |

    | x <- 400, y <- 300, b <- 50, c <- 0 |

    [ :break |
        ren setR: 20 G: 20 B: 20.
        ren clear.

        ren setR: 0 G: 255 B: 0.
        ren drawLineX: 0 Y: 0 X: x Y: y.
        ren drawLineX: win width Y: 0 X: x Y: y.
        ren drawLineX: 0 Y: win height X: x Y: y.
        ren drawLineX: win width Y: win height X: x Y: y.
        ren fillRectX: 0 Y: 0 W: 50 H: 50.

        (c > 0) -> [ c <- c - 1. ren setR: 255 G: 0 B: 0 ].
        ren drawLineX: (x - b) Y: (y - b) X: (x + b) Y: (y - b).
        ren drawLineX: (x + b) Y: (y - b) X: (x + b) Y: (y + b).
        ren drawLineX: (x - b) Y: (y + b) X: (x + b) Y: (y + b).
        ren drawLineX: (x - b) Y: (y - b) X: (x - b) Y: (y + b).
        
        ren present.
        ((event poll) != 0) -> [
            (event isMouseMotionEvent) -> [
                x <- event mouseMotionX.
                y <- event mouseMotionY.
            ].
            (event isMouseButtonDown) -> [
                c <- 500.
            ].
            (event isKeyDown) -> [
                break value. "Stop the program for now"
            ].
        ].
    ] repeatWithBreak.
    (io out << 'Done!') newline commit.
).
