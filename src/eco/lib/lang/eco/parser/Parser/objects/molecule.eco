
#=lang/eco/parser/Parser/Molecule: {
    static with inherited _ = #<:std/Module>;

    parseObjectSlotFlags: flags => (
        [
            begin
              if: [ (check: TokenType DEPRECATED) isYes ] then: [ flags setDeprecated ] ~
              if: [ (check: TokenType PROTECTED)  isYes ] then: [ flags setProtected  ] ~
              if: [ (check: TokenType STATIC)     isYes ] then: [ flags setStatic     ] ~
              if: [ (check: TokenType PRIVATE)    isYes ] then: [ flags setPrivate    ] ~
              if: [ (check: TokenType FINAL)      isYes ] then: [ flags setFinal      ] ~
              if: [ (check: TokenType WITH)       isYes ] then: [ flags setWith       ] ~
              if: [ (check: TokenType OWN)        isYes ] then: [ flags setOwn        ] ~
              if: [ (check: TokenType INHERITED)  isYes ] then: [ flags setInherited  ] ~
            default: [ ^ flags ]
        ] repeat
    );

    parseSlotWithBuilder: builder => (
        | flags <- builder openFlags |
        parseObjectSlotFlags: flags.

        |
            type     <- parseOptionalType,
            decl     <- parseMethodDeclaration,
            slotname <- decl first,
            arglist  <- decl second,
            varargs  <- decl third
        |

        (check: TokenType RARROW) yes: [
            | body <- parseExpression |
            builder addCodeSlotWithFlags: flags
                                    Type: type
                                    Name: slotname
                                    Args: arglist
                                    Varargs: varargs
                                    Body: body
        ] no: [
            | value |
            (check: TokenType ASSIGNMENT) yes: [ value <- parseExpression ]
                                           no: [ value <- newASTSelf ].
            builder addValueSlotWithFlags: flags
                                     Type: type
                                     Name: slotname
                                    Value: value
        ]
    );

    parseMolecule => (
        | builder <- newASTMoleculeBuilder |
        parseMoleculeUntil: TokenType RCURLY WithBuilder: builder.
      ^ builder commit
    );

    parseMoleculeUntil: stop WithBuilder: builder => (
        [ :break |
            begin
              if: [ (check: stop)             isYes ] then: [ break value                                               ] ~
              if: [ (check: TokenType LPAREN) isYes ] then: [ parseMoleculeUntil: TokenType RPAREN WithBuilder: builder ] ~
              if: [ (check: TokenType CARET)  isYes ] then: [ builder addParent: parseExpression                        ] ~
            default: [ parseSlotWithBuilder: builder ].
            (check: stop) yes: [ break value ].
            expect: TokenType SEPARATOR
        ] repeatWithBreak.
    );
}
