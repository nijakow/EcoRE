
#=std/util/WeakMap: {
    with inherited _ = #<:std/Clonable>;

    protected pairs;
    protected comparator;

    with inherited trait = {
        static with inherited _ = #<:std/Trait>;

        initWithComparator: _comparator => (
            pairs      <- std util newVector.
            comparator <- _comparator.
          ^ self
        );
        init => initWithComparator: std util comparators DefaultComparator;

        forEach: block => (
            pairs do: [ :pair |
                | v <- (pair first) get |
                (((pair first) empty) not) if: [
                    block value(v, pair second).
                ].
            ].
          ^ self
        );
        do: block => forEach: [ :k, :v | block value(k) ];

        from: collection => ( collection forEach: [ :k, :v | at: k put: v ]. ^ self );
        into: collection => collection from: self;

        at: index => ( forEach: [ :k, :v | (comparator value(k, index)) if: [ ^ Yes: v ] ]. No );

        at: index put: value => (
            pairs do: [ :pair |
                | v <- (pair first) get |
                (((pair first) empty) not) if: [
                    (comparator value(v, index)) if: [ p second: value. ^ self ].
                ].
            ].
            pairs append: ((std util WeakBox: index) ~> value).
          ^ self
        );

        keysInto: collection => ( forEach: [ :k, :v | collection add: k ]. ^ collection );
        valuesInto: collection => ( forEach: [ :k, :v | collection add: v ]. ^ collection );

        keys => keysInto: (std util newSet);
        values => valuesInto: (std util newSet);

        writeOn: w => w << 'a weak map';
    };
}
