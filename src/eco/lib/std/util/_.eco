"
    util.eco - The `std util` package of the Ecosphere
"

#=std/util/StandardListIterations: {
    do: body => (
        0 for: size do: [ :i | body value(at: i) ].
      ^ self
    );

    forEach: body => (
        0 for: size do: [ :i | body value(i, at: i) ].
      ^ self
    );

    reverseDo: block => (
        | i <- size - 1 |
        [ i >= 0 ] while: [
            block value(at: i).
            i <- i - 1.
        ].
      ^ self
    );
},

#=std/util/SafeListConversions: {
    into: other => other from: self;
    from: other => ( other do: [ :e | add: e ]. ^ self);

    map: f => (
        | v <- std util newVector |
        do: [ :e | v add: (f value(e)) ].
      ^ v asArray
    );

    asArray  => asVector asArray;
    asVector => into: (std util newVector);
},

#=std/util/StandardListConversions: {
    into: other => other from: self;
    from: other => ( other do: [ :e | add: e ]. ^ self);

    map: f => (
        | v <- std util newVector |
        do: [ :e | v add: (f value(e)) ].
      ^ v asArray
    );

    asArray  => into: (std util newArray: size);
    asBlob   => into: (std util newBlob: size);
    asVector => into: (std util newVector);
},

#=std/util/StandardListAccess: {
    first => at: 0;
    second => at: 1;
    third => at: 2;
    fourth => at: 3;
    fifth => at: 4;
    sixth => at: 5;
    seventh => at: 6;
    eighth => at: 7;
    ninth => at: 8;
    tenth => at: 9;
    last => at: (size - 1);

    let1: block => block value(first);
    let2: block => block value(first, second);
    let3: block => block value(first, second, third);
    let4: block => block value(first, second, third, fourth);
},

#=std/util/StandardListSearch: {
    find: element => (
        forEach: [ :i, :v | (std util comparators DefaultComparator value(v, element)) if: [ ^ Yes: i ] ].
        ^ No
    );

    contains: element => (
        forEach: [ :i, :v | (std util comparators DefaultComparator value(v, element)) if: [ ^ true ] ].
      ^ false
    );

    contains?: element => contains: element;
},

#=std/util/SizedIndexable: {
    static with inherited _ = #<:std/Trait>;

    with inherited iterations = #<:std/util/StandardListIterations>;
    with inherited conversions = #<:std/util/StandardListConversions>;
    with inherited access = #<:std/util/StandardListAccess>;
    with inherited search = #<:std/util/StandardListSearch>;

    iterator => (std util iterators IndexIterator) new: self;
};

#=std/util/traits: {
    static with inherited _ = #<:std/Module>;

    SizedIndexable = #<:std/util/SizedIndexable>;
};

#=std/util: {
    static with inherited _ = #<:std/Module>;
    
    iterators = #<:std/util/iterators>;
    comparators = #<:std/util/comparators>;

    traits = #<:std/util/traits>;

    text = #<:std/util/text>;
    graphics = #<:std/util/graphics>;

    newArray(...) => #'ecosphere.ecore.object.array.array'(...);
    newArray: size => #'ecosphere.ecore.object.array.new'(size);
    newBlob:  size => #'ecosphere.ecore.object.blob.new'(size);
    newByteVector  => (#<:std/util> ByteVector) new;
    newVector      => (#<:std/util> Vector) new;
    newStack       => (#<:std/util> Stack) new;
    newQueue       => (#<:std/util> Queue) new;
    newSet         => (#<:std/util> Set) new;
    newMap         => (#<:std/util> Map) new;
    newAlist       => newMap;
    newAlistWithComparator: comp => (#<:std/util> Map clone) initWithComparator: comp;
    newRangeFrom: start To: end => ((#<:std/util> Range) clone) initWithStart: start End: end;
    newPair(a, b) => ((#<:std/util> MutablePair) clone) initWithFirst: a Second: b;
    newBox => (#<:std/util> Box) new;
    newWeakSet => (#<:std/util> WeakSet) new;
    newWeakMap => (#<:std/util> WeakMap) new;

    WriteIterable: it On: writer => (
        writer << '#('.
        it forEach: [ :i, :e | (i > 0) if: [ writer << ', ' ]. writer << e ].
        writer << ')'
    );

    WeakBox: e => #'ecosphere.ecore.object.weakbox.new'() ~ set: e;
    Box: e => (#<:std/util> Box) new: e;

    Stack = #<:std/util/Stack>;
    Queue = #<:std/util/Queue>;
    Set = #<:std/util/Set>;
    Vector = #<:std/util/Vector>;
    ByteVector = #<:std/util/ByteVector>;
    Collector = #<:std/util/Collector>;
    MutablePair = #<:std/util/MutablePair>;
    Map = #<:std/util/Map>;
    Range = #<:std/util/Range>;
    Box = #<:std/util/Box>;
    WeakSet = #<:std/util/WeakSet>;
    WeakMap = #<:std/util/WeakMap>;
}
