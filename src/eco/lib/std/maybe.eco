
#=std/maybe: {
    Yes = #=std/maybe/Yes: {
        static with inherited _ = #<:std/Clonable>;

        yes;

        with inherited trait = {
            static with inherited _ = #<:std/Trait>;

            init: v => yes <- v;
            yes: block => block value(yes);
            no: block => self;
            yes: yblock no: nblock => yblock value(yes);
            no: nblock yes: yblock => yblock value(yes);

            or: other => self;

            isYes => true;
            isNo  => false;

            ifYes: block => block value(self);
            ifNo:  block => self;

            ??: v => yes;

            writeOn: writer => writer << 'Yes: ' << yes;
        };
    };

    Yes: value => (#<:std/maybe/Yes> clone) init: value;

    No = #=std/maybe/No: {
        static with inherited _ = #<:std/Singleton>;

        with inherited trait = {
            static with inherited _ = #<:std/Trait>;
            
            yes: block => self;
            no: block => block value;
            yes: yblock no: nblock => nblock value;
            no: nblock yes: yblock => nblock value;
            yes => std sys error ThrowMessage: 'Expected a value, got No';
            or: other => other;

            isYes => false;
            isNo  => true;

            ifYes: block => self;
            ifNo:  block => block value(self);

            ??: v => v;

            writeOn: writer => writer << 'No';
        };
    };
}
