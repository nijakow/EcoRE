
#=std/bool: {
    true = #=std/bool/true: {
        static with inherited _ = #<:std/Clonable>;
        
        with inherited trait = {
            static with inherited _ = #<:std/Trait>;

            asBool           => self;
            not              => false;
            -> block         => block value;
            if: block        => block value;
            unless: block    => self;
            if: t else: f    => t value;
            true: t false: f => if: t else: f;
            true: t          => if: t;
            false: f         => unless: f;
            if: c then: b    => ( (c value) if: [ b value. ^ false ] else: [ ^ self ] );
            default: b       => ( b value. ^ self );

            and: other       => other;
            or:  other       => self;

            writeOn: writer  => writer << 'true';
        };
    },

    false = #=std/bool/false: {
        static with inherited _ = #<:std/Clonable>;
        
        with inherited trait = {
            static with inherited _ = #<:std/Trait>;
            
            asBool           => self;
            not              => true;
            -> block         => self;
            if: block        => self;
            unless: block    => block value;
            if: t else: f    => f value;
            true: t false: f => if: t else: f;
            true: t          => if: t;
            false: f         => unless: f;
            if: c then: b    => self;
            default: b       => self;

            and: other       => self;
            or:  other       => other;

            writeOn: writer  => writer << 'false';
        };
    }
}
