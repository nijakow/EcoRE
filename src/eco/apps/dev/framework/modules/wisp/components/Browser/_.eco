
std Module
{
    Behavior <- ($package modules polymorphic) NewBehavior
                    ~ name: 'a browser'
                    ~ presenterFunction: $[ :stream :kernel |
                            ((kernel payload) current) yes: [ :current |
                                stream emit: current.
                            ].
                      ]
                    ~ enableBlessing
                    ~ build;
    
    State :: Classlike
    {
        Instance :: Clonable
        {
            |
                maybeMorphKernel <- No,
                current          <- No,
                views,
            |

            {
                protected wisp        <- $package;
                protected polymorphic <- $package modules polymorphic;
                protected ui          => polymorphic ui;

                init => (
                    |
                        navigator <- (wisp components Navigator) Browser: self,
                        view      <- (wisp components BrowserView)
                                            Object: self
                                         Navigator: navigator,
                    |

                    maybeMorphKernel <- No.
                    current          <- No.
                    views            <- std util newStack.

                    views push: view.
                    rebuild.

                  ^ self
                );

                protected current: c => current <- Yes: c;

                bless: morph => maybeMorphKernel <- Yes: morph;

                protected changed => (
                    maybeMorphKernel yes: [ :morph |
                        morph contentChanged.
                    ].
                  ^ self
                );

                navigateToObject: object Navigator: navigator => (
                    |
                        view <- (wisp components BrowserView)
                                    Object: object
                                 Navigator: navigator,
                    |

                    views push: view.
                    current: view.
                    rebuild.
                    changed.

                  ^ self
                );

                navigateBack => (
                    ((views empty?) not) if: [
                        views pop.
                        ((views empty?) not) if: [
                            current: (views top).
                        ] else: [
                            current <- No.
                        ].
                        rebuild.
                        changed.
                    ].

                  ^ self
                );

                rebuild => (
                    current: (
                        ui VFlex(
                            ui Margin: 15 Child: (
                                (views empty?)
                                      if: [ ui Label: 'Nothing to show' ]
                                    else: [ (views top) morph           ],
                            ),
                            ui HFlex(
                                (ui Button) Label: '<--'   Action: $[ :(browser) | browser navigateBack ] bind(self),
                                (ui Button) Label: 'Grab'  Action: $[],
                                (ui Button) Label: 'Proxy' Action: $[],
                                (ui Button) Label: 'Shell' Action: $[],
                            ) ~ yLayoutShrinkWrap!,
                        )
                    ).
                  ^ self
                );

                writeOn: w => w << 'a browser state';
            }
        };

        New => Instance new;
    };

    New => Behavior link: (State New);
}
