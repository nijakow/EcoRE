
{
    isa std Classlike;

    Instance :: Clonable
    {
        |
            maybeContext       <- No,
            maybeFixedBounds   <- No,
            minX               <- 0,
            minY               <- 0,
            maxX               <- 0,
            maxY               <- 0,
            presentations,
            cachedMorphs,
            node
        |

        {
            protected polymorphic <- $package;

            initWithMaybeFixedBounds: _fixed => (
                maybeContext       <- Yes: (polymorphic DefaultRenderingContext).
                maybeFixedBounds   <- _fixed.
                presentations      <- std util newVector.
                cachedMorphs       <- std util newVector.
                node               <- ((polymorphic core morph) tree nodes) VoidNode.
                minX               <- 0.
                minY               <- 0.
                maxX               <- 0.
                maxY               <- 0.
              ^ self
            );

            realWidth  => maxX - minX;
            realHeight => maxY - minY;
            realBounds => (polymorphic math geometry Rectangle)
                              X: minX
                              Y: minY
                              W: realWidth
                              H: realHeight;

            realExtent  => ((polymorphic math geometry) Point) X: maxX Y: maxY;
            maybeExtent => (maybeFixedBounds) yes: [ :mb | Yes: mb extent ] no: [ No ];

            node:    n => node <- n;
            context: c => maybeContext <- Yes: c;

            changeFixedBounds: newFixedBounds => (
                maybeFixedBounds <- Yes: newFixedBounds.
                maxX             <- maxX max: ((newFixedBounds corner) x).
                maxY             <- maxY max: ((newFixedBounds corner) y).
              ^ self
            );

            claimBounds: bounds => (
                |
                    topLeft     <- bounds origin,
                    bottomRight <- bounds corner
                |

                minX <- minX min: topLeft x.
                minY <- minY min: topLeft y.

                maxX <- maxX max: bottomRight x.
                maxY <- maxY max: bottomRight y.

              ^ self
            );

            presentationsDo: block => presentations do: block;
            morphsDo: block => (
                presentationsDo: [ :presentation |
                    (presentation morphPresentation?)
                      ~ if: [ block value(presentation morph) ]
                ].
              ^ self
            );

            presentationsReverseDo: block => presentations reverseDo: block;
            morphsReverseDo: block => (
                presentationsReverseDo: [ :presentation |
                    (presentation morphPresentation?)
                      ~ if: [ block value(presentation morph) ]
                ].
              ^ self
            );

            lookupKernel: kernel => (
                cachedMorphs forEach: [ :i :each |
                    (each kernel == kernel) if: [
                        cachedMorphs erase: i.
                      ^ Yes: each
                    ].
                ].
              ^ No
            );

            add: presentation => (
                presentation connectToParentNode: node. "Connect the node of the presented object to the paper's node..."
                claimBounds: presentation bounds.
                presentations add: presentation.
                (presentation morphPresentation?) if: [ (presentation morph) enable ].
              ^ self
            );

            addOld: presentation => (
                (presentation morphPresentation?) if: [ (presentation morph) enable ].
                claimBounds: presentation bounds.
                presentations add: presentation.
              ^ self
            );

            protected destroyCachedMorphs => (
                cachedMorphs do: [ :each | each disconnectFromParentNode ].
                cachedMorphs <- std util newVector. "New vector to make sure the old vector doesn't keep any resources alive"
              ^ self
            );

            protected clearPresentations => (
                "Throw out the old presentations, but keep the references to the morphs."
                presentations do: [ :each |
                    (each morphPresentation?) if: [
                        (each morph) disable.
                        cachedMorphs add: (each morph).
                    ] else: [
                        each disconnectFromParentNode.
                    ].
                ].

                presentations clear.

              ^ self
            );

            destroy => (
                clearPresentations.
                destroyCachedMorphs.
              ^ self
            );

            clear => (
                "Clear the old morph cache, if it exists."
                ((cachedMorphs empty?) not) if: [
                    destroyCachedMorphs.
                ].

                clearPresentations.

                minX <- 0.
                minY <- 0.
                maxX <- 0.
                maxY <- 0.

              ^ self
            );

            clearAndReset => (
                self clear.
                self maybeFixedBounds <- No.
              ^ self
            );

            emitter => ((polymorphic core stream) Emitter) Paper: self;
            stream  => ((polymorphic core stream) Stream) Emitter: emitter;

            renderOn: renderer => (
                presentations do: [ :each | each renderOn: renderer ].
              ^ self
            );

            "For now, ignore viewport and just render everything."
            renderOn: renderer Viewport: viewport => renderOn: renderer;

            writeOn: w => w << 'a paper';
        }
    };

    FixedBounds: fixedBounds => (Instance clone) initWithMaybeFixedBounds: (Yes: fixedBounds);
    New                      => (Instance clone) initWithMaybeFixedBounds: No;
}
