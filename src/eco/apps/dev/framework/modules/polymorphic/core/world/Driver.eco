
{
    isa std Classlike;

    Instance :: std Clonable
    {
        |
            world,
            rootPaper,
            rootMorph,
            dispatcher,

            cachedMouseX <- 0,
            cachedMouseY <- 0,
        |

        {
            protected polymorphic <- $package;

            initWithWorld: w => (
                world      <- w.
                rootPaper  <- ((polymorphic core paper) Paper) FixedBounds: ((polymorphic math geometry) Rectangle W: 400 H: 400).
                rootMorph  <- (((polymorphic morphs) repo system) Session) Driver: self.
                dispatcher <- ((polymorphic core event) dispatching Dispatcher) Driver: self.

                rootPaper node: (world node).
                (world node) paper: rootPaper.

                rootPaper context: ((polymorphic core) RenderingContext) New.

              ^ self
            );

            initWithoutWorld => initWithWorld: (((polymorphic core world) World) New);

            session => self;

            rootPayload => rootMorph payload;

            extent: extent => (
                rootPayload extent: extent.
              ^ self
            );

            mouseMovedToX: x Y: y => (
                cachedMouseX <- x,
                cachedMouseY <- y.

                dispatcher
                    mouseMovedToX: x
                                Y: y.
                
                (rootPayload hand)
                    moveToX: x
                          Y: y.
                
              ^ self
            );

            mouseClicked: click => (
                dispatcher
                    mouseClickedAtX: cachedMouseX
                                  Y: cachedMouseY
                              Click: click.
              ^ self
            );

            mouseReleased: click => (
                (rootPayload hand)
                    tryRelease.
                "dispatcher
                    mouseReleasedAtX: cachedMouseX
                                   Y: cachedMouseY
                               Click: click."
              ^ self
            );

            mouseScrolledByX: dx Y: dy => (
              ^ self
            );

            keyPressed: key => (
                dispatcher
                    keyPressedAtX: cachedMouseX
                                Y: cachedMouseY
                              Key: key.
              ^ self
            );

            keyReleased: key => (
                dispatcher
                    keyReleasedAtX: cachedMouseX
                                 Y: cachedMouseY
                               Key: key.
              ^ self
            );

            tick => (
                world tickAll.

                rootPaper clear.
                (rootPaper stream) withGraphicsDo: [ :graphics |
                    graphics in: (rootPayload bounds)
                            put: rootMorph.
                ].
                
              ^ self
            );

            writeOn: w => w << 'a polymorphic driver';
        }
    };

    World: world => (Instance clone) initWithWorld: world;
    New          => (Instance clone) initWithoutWorld;
}
