
{
    isa std Classlike;

    protected polymorphic <- $package;

    Instance :: Clonable
    {
        |
            paper,
            origin,
            maybeExtent
        |

        {
            protected polymorphic <- $package;

            paper: p origin: o maybeExtent: e => (
                paper       <- p.
                origin      <- o.
                maybeExtent <- e.
              ^ self
            );

            extent => maybeExtent yes;
            bounds => (polymorphic math geometry Rectangle) Origin: origin Extent: extent;

            emitDrawable: drawable In: bounds => (
                | presentation <- (polymorphic core paper presenting DrawablePresentation) Object: drawable Bounds: bounds |

                paper add: presentation.

              ^ self
            );

            emitDrawable: drawable At: offset => (
                | presentation <- (polymorphic core paper presenting DrawablePresentation) Object: drawable Offset: offset |

                paper add: presentation.

              ^ self
            );

            emitMorph: kernel In: bounds => (
                |
                    boundsInfo <- ((polymorphic core morph) BoundsInfo) Bounds: bounds,
                |

                (paper lookupKernel: kernel) yes: [ :morph |
                    morph changeBounds: boundsInfo.

                    |
                        presentation <- ((polymorphic core paper) presenting MorphPresentation) Morph: morph
                    |

                    paper addOld: presentation.
                ] no: [
                    |
                        morph        <- kernel instantiateInBoundsInfo: boundsInfo,
                        presentation <- ((polymorphic core paper) presenting MorphPresentation) Morph: morph
                    |

                    paper add: presentation.
                ].
              ^ self
            );

            claimBounds: bounds => (
                paper claimBounds: (bounds translateBy: origin).
              ^ self
            );

            writeOn: w => w << 'an emitter';
        }
    };

    Paper: paper Origin: origin MaybeExtent: extent => (Instance clone) paper: paper origin: origin maybeExtent: extent;
    Paper: paper Origin: origin Extent: extent      => Paper: paper Origin: origin MaybeExtent: (Yes: extent);
    Paper: paper Origin: origin                     => Paper: paper Origin: origin MaybeExtent: (paper maybeExtent);
    Paper: paper                                    => Paper: paper Origin: ((polymorphic math geometry Point) Origin);
}
