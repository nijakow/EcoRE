
{
    isa std Classlike;

    protected polymorphic <- $package;

    Instance :: Clonable
    {
        |
            paper,
            origin,
            maybeExtent
        |

        {
            protected polymorphic <- $package;

            paper: p origin: o maybeExtent: e => (
                paper       <- p.
                origin      <- o.
                maybeExtent <- e.
              ^ self
            );

            extent => maybeExtent yes;
            bounds => (polymorphic math geometry Rectangle) Origin: origin Extent: extent;

            graphicsWithOrigin: origin MaybeExtent: extent => (polymorphic core stream Graphics) Emitter: self Origin: origin MaybeExtent: extent;
            graphicsForBounds: bounds                      => graphicsWithOrigin: (bounds origin) MaybeExtent: (Yes: (bounds extent));

            streamAtX: x Y: y         => (polymorphic core stream Stream) Emitter: self X: x Y: y;
            streamAt: point           => (polymorphic core stream Stream) Emitter: self At: point;
            stream                    => (polymorphic core stream Stream) Emitter: self;

            emitDrawable: drawable In: bounds => (
                | presentation <- (polymorphic core paper presenting DrawablePresentation) Object: drawable Bounds: bounds |

                paper add: presentation.

              ^ self
            );

            emitMorph: kernel In: bounds => (
                "TODO: Lookup morph kernel in the paper cache"

                | presentation <- (polymorphic core paper presenting MorphPresentation) Kernel: kernel Bounds: bounds |

                paper add: presentation.

              ^ self
            );

            writeOn: w => w << 'an emitter';
        }
    };

    Paper: paper Origin: origin MaybeExtent: extent => (Instance clone) paper: paper origin: origin maybeExtent: extent;
    Paper: paper Origin: origin Extent: extent      => Paper: paper Origin: origin MaybeExtent: (Yes: extent);
    Paper: paper Origin: origin                     => Paper: paper Origin: origin MaybeExtent: (paper maybeExtent);
    Paper: paper                                    => Paper: paper Origin: ((polymorphic math geometry Point) Origin);
}
