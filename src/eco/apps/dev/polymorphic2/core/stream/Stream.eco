{
    isa std Classlike;

    Instance :: Clonable
    {
        |
            emitter,

            xOffset <- 0,
            yOffset <- 0,

            nextLineDelta <- 0,
            protected minNextLineDelta <- 0
        |

        {
            protected polymorphic <- $package;

            emitter: e x: x y: y => (
                emitter <- e.
                xOffset <- x.
                yOffset <- y.
                nextLineDelta <- 0,
                minNextLineDelta <- ((polymorphic gfx eve) DefaultFont) height.
              ^ self
            );

            protected cursor => (polymorphic math geometry Point) X: xOffset Y: yOffset;
            protected bounds => emitter bounds;

            maybeExtent => emitter maybeExtent;

            protected graphicsWithOrigin: origin MaybeExtent: extent => (polymorphic core stream Graphics) Emitter: emitter Origin: origin MaybeExtent: extent;
            protected graphicsInBounds: bounds                       => graphicsWithOrigin: (bounds origin) MaybeExtent: (Yes: (bounds extent));
            
            protected graphicsExtent: extent => (
                | currentCursor <- cursor |
                reserveExtent: extent.
              ^ graphicsInBounds: (currentCursor extent: extent)
            );

            protected reserveExtentX: x Y: y => (
                xOffset <- xOffset + x.
                nextLineDelta <- nextLineDelta max: y.
              ^ self
            );

            protected reserveExtent: extent => reserveExtentX: (extent x) Y: (extent y);

            withGraphicsDo: block => block value(graphicsWithOrigin: (emitter origin) MaybeExtent: (emitter maybeExtent));
            withGraphicsInBounds: bounds Do: block => block value(graphicsInBounds: bounds);
            withGraphicsAtX: x Y: y W: w H: h Do: block => block value(graphicsInBounds: ((polymorphic math geometry Rectangle) X: x Y: y W: w H: h));
            withRoom: extent ForGraphicsDo: block  => block value(graphicsExtent: extent);

            emitDrawable: object => (
                |
                    objectBounds  <- object bounds,
                    currentX      <- xOffset,
                    currentY      <- yOffset
                |
                reserveExtentX: (objectBounds w) Y: (objectBounds h).
                emitter emitDrawable: object At: ((polymorphic math geometry Point) X: (currentX - objectBounds x) Y: (currentY - objectBounds y)).
              ^ self
            );

            emitMorphKernel: kernel => (
                |
                    objectMinBounds <- kernel minBounds,
                    currentX        <- xOffset,
                    currentY        <- yOffset,
                    displacement    <- ((polymorphic math geometry) Point)
                                            X: currentX - (objectMinBounds origin) x
                                            Y: currentY - (objectMinBounds origin) y
                |
                reserveExtentX: (objectMinBounds w) Y: (objectMinBounds h).
                emitter emitMorph: kernel BoundsInfo: (kernel offsetBy: displacement).
              ^ self
            );

            emit: object => object emitOnStream: self;

            << object => (
                | sw <- (std io StringWriter) new |

                sw << object.

                self emit: ((polymorphic core objects Text) Caption: (sw build)).

              ^ self
            );

            newline => (
                xOffset <- emitter origin x.
                yOffset <- yOffset + (nextLineDelta max: minNextLineDelta).
                nextLineDelta <- 0.
              ^ self
            );

            commit => self;
        }
    };

    Emitter: emitter X: x Y: y => (Instance clone) emitter: emitter x: x y: y;
    Emitter: emitter At: point => Emitter: emitter X: (point x) Y: (point y);
    Emitter: emitter           => Emitter: emitter X: (emitter origin x) Y: (emitter origin y);
}
