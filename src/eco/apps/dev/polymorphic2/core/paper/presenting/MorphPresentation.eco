
{
    isa std Classlike;

    Instance :: Clonable
    {
        |
            bounds,
            kernel,
            paper,

            parent <- No,

            protected dirty? <- true
        |

        {
            with inherited Presentation <- $package;

            bounds: b kernel: k paper: p => (
                bounds <- b.
                kernel <- k.
                paper  <- p.

                parent <- No.

                p parentPresentation: self.

              ^ self
            );

            bounds: b kernel: k => bounds: b kernel: k paper: ((polymorphic core paper Paper) New);

            morphPresentation? => true;

            noParent => (
                parent <- No.
              ^ self
            );

            parent: newParent => (
                self noParent.
                parent <- Yes: newParent.
              ^ self
            );

            protected _redraw => (
                kernel drawOnStream: (paper stream) Instance: self.
                dirty? <- false.
              ^ self
            );

            redrawInPlace => (
                paper clear.
                _redraw.
              ^ self
            );
            redrawFreely  => (
                paper clearAndReset.
                _redraw.
                "TODO: If the size of the paper has changed, we need to forward the change to the parent presentation."
              ^ self
            );

            protected ensureNotDirty => (
                dirty? -> [
                    _redraw.
                ].
              ^ self
            );

            renderOn: renderer => (
                ensureNotDirty.
                renderer withPushedRelativeOffset: offset Do: [
                    paper renderOn: renderer.
                ].
              ^ self
            );

            writeOn: w => w << 'a morph presentation';
        }
    };

    Kernel: kernel Bounds: bounds Paper: paper => (Instance clone) bounds: bounds kernel: kernel paper: paper;
    Kernel: kernel Bounds: bounds              => (Instance clone) bounds: bounds kernel: kernel;
}
