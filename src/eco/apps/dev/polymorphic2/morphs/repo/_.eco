
std Module
{
    TestMorph <- ($package core morph Behavior) New
                ~ presenter:
                    {
                        isa std Singleton;

                        with inherited PresenterTrait <- ($package core morph behavior presenting) PresenterTrait;

                        drawOn: stream Kernel: kernel => (
                            stream emit: ((polymorphic core objects Rectangle) X: 0 Y: 0 W: 64 H: 64).
                          ^ self
                        );

                        writeOn: w => w << 'the test presenter';
                    };
    
    EmitMorph <- ($package core morph Behavior) New
                ~ presenter:
                    {
                        isa std Singleton;

                        with inherited PresenterTrait <- ($package core morph behavior presenting) PresenterTrait;

                        drawOn: stream Kernel: kernel => (
                            stream emit: ((kernel state) payload).
                          ^ self
                        );

                        writeOn: w => w << 'the emit presenter';
                    };

    FlexMorph :: Module
    {
        protected polymorphic <- $package;

        Behavior <- ($package core morph Behavior) New
                  ~ presenter:
                      {
                        isa std Singleton;

                        with inherited PresenterTrait <- ($package core morph behavior presenting) PresenterTrait;

                        drawOn: stream Kernel: kernel => (
                            |
                                orientation          <- ((kernel state) payload) orientation,
                                morphs               <- ((kernel state) payload) morphs,
                                mainAxisOrientation  <- orientation,
                                crossAxisOrientation <- orientation opposite,
                                mainAxisSize         <- 0,
                                crossAxisSize        <- 0,
                                flexCount            <- 0,
                                morphCount           <- morphs size,
                                assignedSize         <- 0,
                            |

                            "Compute the size of the main axis and the cross axis."
                            morphs do: [ :morph |
                                |
                                    morphMainAxisExtent  <- (morph minExtentForOrientation: mainAxisOrientation),
                                    morphCrossAxisExtent <- (morph minExtentForOrientation: crossAxisOrientation),
                                |

                                mainAxisSize  <- mainAxisSize + morphMainAxisExtent.
                                crossAxisSize <- crossAxisSize max: morphCrossAxisExtent.

                                (morph layoutForOrientation: mainAxisOrientation) flexible?
                                  ~ if: [ flexCount <- flexCount + 1 ]
                                  else: [ assignedSize <- assignedSize + morphMainAxisExtent ].
                            ].

                            |
                                flexSize <- (flexCount zero?) if: [ 0 ] else: [ ((mainAxisSize - assignedSize) asFloat / flexCount asFloat) asInt ],
                            |

                            morphs do: [ :morph |
                                "Very simple for now..."
                                stream emit: morph.
                                (orientation vertical?) -> [
                                    stream newline.
                                ].
                            ].

                          ^ self
                        );

                        writeOn: w => w << 'the flex presenter';
                      };
        
        Payload :: Classlike
        {
            Instance :: Clonable
            {
                |
                    orientation,
                    morphs
                |

                {
                    protected polymorphic <- $package;

                    initWithOrientation: o => (
                        orientation <- o.
                        morphs      <- std util newVector.
                      ^ self
                    );
                }
            };

            Orientation: orientation => (Instance clone) initWithOrientation: orientation;
        };

        ForOrientation: orientation => (Behavior link: (Payload Orientation: orientation));

        Horizontal => ForOrientation: (polymorphic math geometry orientation) Horizontal;
        Vertical   => ForOrientation: (polymorphic math geometry orientation) Vertical;
    };
}
