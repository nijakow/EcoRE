
std Module
{
    Behavior <- ($package core) NewBehavior
                    ~ name: 'a hand morph'
                    ~ presenterFunction: $[ :(polymorphic) :stream :kernel |
                        ((kernel payload) grabbedMorph) yes: [ :gm |
                            stream emit: gm.
                        ] no: [
                            stream << 'a hand'.
                        ].
                    ] bind($package)
                    ~ enableBlessing
                    ~ build;

    State :: Classlike {
        Instance :: Clonable
        {
            |
                maybeMorphKernel <- No,
                position,
                grabbedMorph,
            |

            {
                protected polymorphic <- $package;

                init => (
                    maybeMorphKernel <- No.
                    position         <- (polymorphic math geometry) Point Origin.
                    grabbedMorph     <- No.
                  ^ self
                );

                protected changed => (
                    maybeMorphKernel yes: [ :kernel | kernel changed ].
                  ^ self
                );

                protected changedPlacement => (
                    maybeMorphKernel yes: [ :kernel | kernel changedPlacement ].
                  ^ self
                );

                bless: kernel => maybeMorphKernel <- Yes: kernel;

                moveToX: x Y: y => (
                    position <- (polymorphic math geometry) Point X: x Y: y.
                    changedPlacement.
                  ^ self
                );

                tryGrab: kernel => (
                    grabbedMorph <- Yes: kernel.
                    changed.
                  ^ self
                );

                tryRelease => (
                    grabbedMorph yes: [ :gm |
                        "TODO"
                        changed.
                      ^ true
                    ].
                  ^ false
                );

                writeOn: w => w << 'a hand';
            }
        };

        New => Instance new;
    };

    New => Behavior link: (State New);
}
