
std Module
{
    Behavior <- ($package core) NewBehavior
                    ~ name: 'a desktop morph'
                    ~ presenterFunction: $[ :(polymorphic) :stream :kernel |
                        stream withGraphicsDo: [ :graphics |
                            (std io out << 'Redrawing desktop! ' << graphics maybeExtent) newline commit.
                            ((kernel payload) windows) forEach: [ :window :winbounds |
                                graphics in: winbounds put: window.
                            ].
                        ].
                    ] bind($package)
                    ~ on: (($package event) predicates AddMorph) do: $[ :kernel :event |
                        ((kernel state) payload) openMorph: (event body).
                        kernel changed.
                    ]
                    ~ on: (($package event) predicates DropMorph) do: $[ :context :event |
                        ((context kernel) payload) openMorph: (event body).
                        (context kernel) changed.
                    ]
                    ~ disposeFunction: $[ :kernel :other |
                        (((kernel state) payload) windows) erase: other.
                        kernel changed.
                    ]
                    ~ build;

    State :: Classlike {
        Instance :: Clonable
        {
            |
                maybeMorphKernel <- No,
                extent,
                windows,
            |

            {
                protected polymorphic <- $package;

                init => (
                    maybeMorphKernel <- No.
                    extent           <- (polymorphic math geometry) Point X: 0 Y: 0.
                    windows          <- std util newMap.
                  ^ self
                );

                bless: kernel => maybeMorphKernel <- Yes: kernel;

                protected changed => (
                    maybeMorphKernel yes: [ :kernel | kernel changed ].
                  ^ self
                );

                extent: e => (
                    extent <- e.
                    (std io out << 'Extent changed: ' << e) newline commit.
                    changed.
                  ^ self
                );

                openMorph: kernel => (
                    |
                        minBounds <- kernel minBounds,
                        newBounds <- (polymorphic math geometry) Rectangle
                                        X: (extent x / 2) - (minBounds w / 2)
                                        Y: (extent y / 2) - (minBounds h / 2)
                                        W: minBounds w
                                        H: minBounds h
                    |
                    windows at: kernel put: newBounds.
                    changed.
                  ^ self
                );

                writeOn: w => w << 'a desktop';
            }
        };

        New => Instance new;
    };

    New => Behavior link: (State New);
}
