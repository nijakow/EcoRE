
std Module
{
    Behavior <- ($package core) NewBehavior
                    ~ name: 'a window morph'
                    ~ presenterFunction: $[ :(polymorphic) :stream :kernel |
                        stream withGraphicsDo: [ :graphics |
                            (graphics maybeBounds) yes: [ :bounds |
                                graphics at: ((polymorphic math geometry) Point Origin)
                                        put: (((polymorphic core objects) Rectangle)
                                                    X: 0
                                                    Y: 0
                                                    W: (bounds w)
                                                    H: (bounds h)).
                            ].
                            ((kernel payload) child) yes: [ :c | stream emit: c ].
                        ].
                    ] bind($package)
                    ~ on: (($package event) predicates AddMorph) do: $[ :kernel :event |
                        ((kernel state) payload) child: (event body).
                        kernel changed.
                    ]
                    ~ build;

    State :: Classlike {
        Instance :: Clonable
        {
            |
                maybeMorphKernel <- No,
                child            <- No,
                bounds,
            |

            {
                protected polymorphic <- $package;

                init => (
                    maybeMorphKernel <- No.
                    child            <- No.
                    bounds           <- ((polymorphic math geometry) Rectangle) X: 0 Y: 0 W: 0 H: 0.
                  ^ self
                );

                bless: kernel => maybeMorphKernel <- Yes: kernel;

                protected changed => (
                    maybeMorphKernel yes: [ :kernel | kernel changed ].
                  ^ self
                );

                child: c => child <- Yes: c;

                bounds: b => (
                    bounds <- b.
                    changed.
                  ^ self
                );

                extent => bounds extent;

                writeOn: w => w << 'a desktop';
            }
        };

        New => Instance new;
        Child: c => New ~ child: c;
    };

    New      => Behavior link: (State New);
    Child: c => Behavior link: (State Child: c);
}
