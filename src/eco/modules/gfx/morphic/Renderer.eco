
    {
        static with inherited _ = std Clonable;

        width, height,
        protected offsets,
        protected clips,
        protected eve,
        protected font;

        theme;

        with inherited trait = {
            protected morphic = $package;

            initWithEve: _eve X: _x Y: _y W: _w H: _h Theme: _theme => (
                eve         <- _eve.
                width       <- _w.
                height      <- _h.
                offsets     <- std util newStack.
                offsets push: (morphic X: _x Y: _y).
                clips       <- std util newStack.
                clips push: (morphic X: _x Y: _y W: width H: height).
                theme       <- _theme.
                font        <- eve DefaultFont.
              ^ self
            );

            initWithEve: _eve => initWithEve: _eve X: 0 Y: 0 W: (_eve CurrentWidth) H: (_eve CurrentHeight) Theme: ((morphic misc color ColorTheme) new);

            animator => morphic misc animation Animator;

            theme: t => theme <- t;

            offset => offsets top;
            pushOffset: point => offsets push: point;
            popOffset => offsets pop;
            withPushedOffset: offset Do: block => (
                pushOffset: offset.
                block value.
                popOffset.
              ^ self
            );

            clip => clips top;
            withClipW: w H: h Do: block => (
                | rect <- morphic X: xOff Y: yOff W: w H: h |
                clips push: rect.
                eve Clip(xOff, yOff, w, h).
                block value.
                clips pop.
                eve Clip(clip x, clip y, clip w, clip h).
              ^ self
            );
            withClipDisabledDo: block => (
                eve Clip(0, 0, width, height).
                block value.
                eve Clip(clip x, clip y, clip w, clip h).
              ^ self
            );

            xOff => offset x;
            yOff => offset y;

            clear => eve Clear;
            present => eve Render;

            setR: r G: g B: b => eve SetColorRGB(r, g, b);
            setColor: color => (
                setR: color r
                   G: color g
                   B: color b
            );

            drawPointX: x Y: y => eve DrawPoint(xOff + x, yOff + y);
            drawLineX: x1 Y: y1 X: x2 Y: y2 => eve DrawLine(xOff + x1, yOff + y1, xOff + x2, yOff + y2);
            drawArrowX: x1 Y: y1 X: x2 Y: y2 => eve DrawArrow(xOff + x1, yOff + y1, xOff + x2, yOff + y2);
            drawRectX: x Y: y W: w H: h => eve DrawRect(xOff + x, yOff + y, w, h);
            drawRect: rect => drawRectX: rect x Y: rect y W: rect w H: rect h;
            fillRectX: x Y: y W: w H: h => eve FillRect(xOff + x, yOff + y, w, h);
            fillRect: rect => fillRectX: rect x Y: rect y W: rect w H: rect h;
            drawText: text X: x Y: y => eve DrawText(text text, xOff + x, yOff + y, font);
            drawChar: char X: x Y: y => eve DrawChar(char, xOff + x, yOff + y, font);
            drawText: text X: x Y: y Font: font => eve DrawText(text text, xOff + x, yOff + y, font);
            drawString: string X: x Y: y => eve DrawText(string, xOff + x, yOff + y, font);
            drawChar: char X: x Y: y Font: font => eve DrawChar(char, xOff + x, yOff + y, font);
            drawTexture: texture X: x Y: y => texture drawAtX: xOff + x Y: yOff + y;  "TODO: Give the texture a pointer to our renderer"
            
            charWidth: char => eve GetCharWidth(char);
            charHeight: char => eve GetCharHeight(char);
            textWidth: string => eve GetTextWidth(string);
            textHeight: string => eve GetTextHeight(string);

            accept: morph => morph renderOn: self;

            writeOn: w => w << 'a morphic renderer';
        };
    };
