{
    static with inherited _ = std Module;
    framework = $;

    own open = {
        static with inherited _ = std Module;
        static framework = $package;

        Morph: morph => (
            (framework context DefaultWorld)
                  open: morph.
          ^ self
        );

        Morph: morph At: offset => (
            (framework context DefaultWorld)
                  open: morph
                    at: offset.
          ^ self
        );

        OutlinerForObject: object => (
            ((framework modules wisp) actions open) Object: object
        );

        Path: path => (
            (framework actions open) Morph: ((framework legacy MirrorBuilder) OutlinerWindowForPath: path)
        );

        own tools = {
            static with inherited _ = std Module;
            static framework = $package;

            Shell => (
                ((framework modules wisp) actions open) Shell
            );

            FileBrowser => (
                (framework actions open) Path: (std os files home)
            );
        };

        own ui = {
            static with inherited _ = std Module;
            static framework = $package;

            Build: block => (
                block value(framework ui builder)
            );

            BuildAndOpen: block => (
                (framework actions open) Morph: (Build: block)
            );

            BuildAndOpen: block At: offset => (
                (framework actions open) Morph: (Build: block) At: offset
            );

            BuildAndOpenContextMenu: block => (
                BuildAndOpen: [ :ui |
                    | cmb <- (framework utility ContextMenuBuilder) new |
                    
                    block value(cmb, ui).
                    
                    cmb buildUsingUiToolkit: ui
                ] At: ((framework context DefaultHand) globalOffset)
            );

            TextMessageBox: text => (
                (framework actions open) Morph: ((framework legacy MirrorBuilder) TextMessageBox: text)
            );
        };
    };

    own eval = {
        static with inherited _ = std Module;
        static framework = $package;

        DocumentAndShowResult: document Environment: environment => (
            ((framework modules eval) actions)
                  EvalDocument: document InEnvironment: environment
                      OnSuccess: $[ :(framework) :val | (framework actions open) OutlinerForObject: val ] bind(framework)
                      OnFailure: $[ :(framework) :err | (framework actions open) ui TextMessageBox: err ] bind(framework)
        );

        StringAndShowResult: string Environment: environment => (
            ((framework modules eval) actions)
                  EvalString: string InEnvironment: environment
                      OnSuccess: $[ :(framework) :val | (framework actions open) OutlinerForObject: val ] bind(framework)
                      OnFailure: $[ :(framework) :err | (framework actions open) ui TextMessageBox: err ] bind(framework)
        );
    };

    own clipboard = {
        static with inherited _ = std Module;
        static framework = $package;

        CopyObject: object => (
            (framework context) DefaultHand
                ~ copyToClipboard: object
        );
    };
}
