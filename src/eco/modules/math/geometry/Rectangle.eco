
{
    static with inherited _ = std Classlike;
    protected geometry = $package;


    own Rectangle = {
        static with inherited _ = std Clonable;

        origin, corner;

        with inherited trait = {
            static with inherited _ = std Trait;
            protected geometry = $package;

            initWithOrigin: _origin Corner: _corner => (
                origin <- _origin.
                corner <- _corner.
              ^ self
            );

            x => origin x;
            y => origin y;
            w => corner x - origin x;
            h => corner y - origin y;

            extent => (geometry Point X: w Y: h);

            topLeft => origin;
            topCenter => (geometry Point X: (origin x + (w / 2)) Y: origin y);
            topRight => (geometry Point X: corner x Y: origin y);
            leftCenter => (geometry Point X: origin x Y: (origin y + (h / 2)));
            center => (geometry Point X: (origin x + (w / 2)) Y: (origin y + (h / 2)));
            rightCenter => (geometry Point X: corner x Y: (origin y + (h / 2)));
            bottomLeft => (geometry Point X: origin x Y: corner y);
            bottomCenter => (geometry Point X: (origin x + (w / 2)) Y: corner y);
            bottomRight => corner;

            containsPoint: p => (
                ((origin x <= p x) not) if: [ ^ false ].
                ((p x <= corner x) not) if: [ ^ false ].
                ((origin y <= p y) not) if: [ ^ false ].
                ((p y <= corner y) not) if: [ ^ false ].
              ^ true
            );

            translateBy: point => (geometry Rectangle X: (origin x + point x) Y: (origin y + point y) W: w H: h);
            translateToOrigin  => (geometry Rectangle X: 0 Y: 0 W: w H: h); 

            originForOrientation: orientation => orientation selectX: x Y: y;
            dimensionForOrientation: orientation => orientation selectX: w Y: h;

            writeOn: w => w << 'a rectangle(' << origin << ', ' << corner << ')';
            presentOn: w => w << 'a rectangle(' present<< origin << ', ' present<< corner << ')';
        };
    };

    Origin: origin Corner: corner => (Rectangle clone) initWithOrigin: origin Corner: corner;
    Origin: origin W: w H: h => (Rectangle clone) initWithOrigin: origin Corner: (geometry Point X: (origin x + w) Y: (origin y + h));
    Origin: origin Extent: extent => (Rectangle clone) initWithOrigin: origin Corner: origin + extent;

    X: x Y: y W: w H: h      => Origin: (geometry Point X: x Y: y) Corner: (geometry Point X: (x + w) Y: (y + h));
    X: x Y: y Extent: extent => Origin: (geometry Point X: x Y: y) Extent: extent;
}
