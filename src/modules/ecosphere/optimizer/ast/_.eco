{
    static with inherited _ = std Module;

    own AST = {
        static with inherited _ = std Clonable;

        with inherited trait = {
            init => self;

            acceptVisitor: visitor => visitor visitAST: self;

            isAtomic => false;

            writeOn: w => 'AST()';
        };
    };

    own ASTRegister = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        depth, index;

        with inherited trait = {
            initWithDepth: _depth Index: _index => (
                $(AST) init.
                depth <- _depth.
                index <- _index.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTRegister: self;

            equalsRegister: other => [ index == other index ] and: [ depth == other depth ];

            isAtomic => true;

            writeOn: w => w << 'r' << depth << '_' << index;
        };
    };

    own ASTConstant = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected constant;

        with inherited trait = {
            initWithConstant: _constant => (
                $(AST) init.
                constant <- _constant;
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTConstant: self;

            isAtomic => true;

            writeOn: w => w << '(' << constant << ')';
        };
    };

    own ASTCode = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected code;

        with inherited trait = {
            initWithCode: _code => (
                $(AST) init.
                code <- _code.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTCode: self;

            isAtomic => true;

            writeOn: w => w << '[ ... | ' << (code ast) << ']';
        };
    };

    own ASTCompound = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected instructions;

        with inherited trait = {
            initWithInstructions: _instructions => (
                $(AST) init.
                instructions <- _instructions asArray.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTCompound: self;

            writeOn: w => w << '(' << instructions << ')';
        };
    };

    own ASTAssignment = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected lhs;
        protected rhs;

        with inherited trait = {
            initWithLHS: _lhs RHS: _rhs => (
                $(AST) init.
                lhs <- _lhs.
                rhs <- _rhs.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTAssignment: self;

            writeOn: w => w << '(' << lhs << ' <- ' << rhs << ')';
        };
    };

    own ASTSlotAssignment = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected slot;
        protected lhs;
        protected rhs;

        with inherited trait = {
            initWithLHS: _lhs RHS: _rhs Slot: _slot => (
                $(AST) init.
                lhs <- _lhs.
                rhs <- _rhs.
                slot <- _slot.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTSlotAssignment: self;

            writeOn: w => w << '(' << lhs << ' ' << slot << ' <- ' << rhs << ')';
        };
    };

    own ASTBuiltin = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected message;
        protected args;
        protected varargs;

        with inherited trait = {
            initWithMessage: _message Args: _args Varargs: _varargs => (
                $(AST) init.
                message  <- _message.
                args     <- _args asArray.
                varargs  <- _varargs.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTBuiltin: self;

            writeOn: w => (
                w << message.
                w << '('.
                0 to: (args size - 2) do: [ :i |
                    w << (args at: i).
                    w << ', '.
                ].
                w << (args last).
                varargs -> [ w << ', ...' ].
                w << ')'.
            );
        };
    };

    own ASTSend = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected message;
        protected args;
        protected varargs;

        with inherited trait = {
            initWithMessage: _message Args: _args Varargs: _varargs => (
                $(AST) init.
                message  <- _message.
                args     <- _args asArray.
                varargs  <- _varargs.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTSend: self;

            writeOn: w => (
                w << (args first).
                w << ' ' << message.
                (args size > 1) -> [
                    w << '('.
                    1 to: (args size - 2) do: [ :i |
                        w << (args at: i).
                        w << ', '.
                    ].
                    w << (args last).
                    varargs -> [ w << ', ...' ].
                    w << ')'.
                ].
            );
        };
    };

    own ASTResend = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected message;
        protected args;
        protected varargs;

        with inherited trait = {
            initWithMessage: _message Args: _args Varargs: _varargs => (
                $(AST) init.
                message  <- _message.
                args     <- _args asArray.
                varargs  <- _varargs.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTResend: self;

            writeOn: w => (
                w << '$(' << (args first).
                w << ') ' << message.
                (args size > 1) -> [
                    w << '('.
                    1 to: (args size - 2) do: [ :i |
                        w << (args at: i).
                        w << ', '.
                    ].
                    w << (args last).
                    varargs -> [ w << ', ...' ].
                    w << ')'.
                ].
            );
        };
    };

    own ASTReturn = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        protected returnValue;
        protected depth;

        with inherited trait = {
            initWithValue: _value Depth: _depth => (
                $(AST) init.
                returnValue <- _value.
                depth       <- _depth.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTReturn: self;

            writeOn: w => w << '^ ' << returnValue;
        };
    };

    own ASTJumpBack = {
        static with inherited _ = std Clonable;
        with inherited own AST = $ AST;

        with inherited trait = {
            init => (
                $(AST) init.
              ^ self
            );

            acceptVisitor: visitor => visitor visitASTJumpBack: self;

            writeOn: w => w << 'ASTJumpBack()';
        };
    };
}
