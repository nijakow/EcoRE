
{
    static with inherited _ = std Clonable;
    with inherited own Morph = $ Morph;

    protected caret,
    protected lines;

    with inherited trait = {
        static with inherited _ = std Trait;

        protected own Line = {
            static with inherited _ = std Clonable;

            textobj;

            static with inherited trait = {
                static with inherited _ = std Trait;

                initWithTextObj: _textobj => (
                    textobj <- _textobj.
                  ^ self
                );

                text => textobj text;

                width  => textobj width;
                height => textobj height;

                caretX: x      => textobj pointAtCharacterIndex: x;
                charAtPoint: x => textobj characterIndexAtX: x;

                renderOn: renderer X: x Y: y => (
                    renderer setR: 0 G: 182 B: 203.
                    renderer drawText: textobj X: x Y: y.
                );

                writeOn: w => w << 'a text editor line';
            };
        };

        init => (
            $(Morph) init.
            caret <- morphic X: 0 Y: 0.
            lines <- std util newVector.
          ^ self
        );

        baseMinWidth => (
            | w <- 0 |
            lines do: [ :l | w <- w max: l width ].
          ^ w
        );

        baseMinHeight => (
            | h <- 0 |
            lines do: [ :l | h <- h + l height ].
          ^ h
        );

        append: text => (
            lines add: (Line clone initWithTextObj: (morphic Text: text)).
            layoutChanged.
          ^ self
        );

        protected replaceLine: y With: text => (
            | line <- (lines at: y) |
            lines at: y put: (Line clone initWithTextObj: (morphic Text: text)).
            layoutChanged.
            renderDirty.
          ^ self
        );
        protected insertLineAfter: y With: text => (
            lines insert: (Line clone initWithTextObj: (morphic Text: text)) at: (y + 1).
            layoutChanged.
            renderDirty.
          ^ self
        );
        protected removeLine: y => (
            lines erase: y.
            layoutChanged.
            renderDirty.
          ^ self
        );

        protected joinLines: y1 And: y2 => (
            "TODO: Check for negative numbers, y1 < y2 and out of bounds."
            replaceLine: y1 With: ((lines at: y1) text + (lines at: y2) text).
            removeLine: y2.
          ^ self
        );

        deleteCharAtX: x Y: y => (
            (x == 0) if: [
                (y > 0) if: [
                    joinLines: (y - 1) And: y.
                ].
            ] else: [
              |
                  text    <- (lines at: y) text,
                  newtext <- (std io StringWriter) new
              |
              text forEach: [ :i :c |
                  (i != (x - 1)) if: [ newtext << c ].
              ].
              replaceLine: y With: newtext build.
            ].
          ^ self
        );

        deleteChar => (
            |
                nextX <- ((caret x) - 1),
                nextY <- (caret y)
            |

            ((caret x) == 0) if: [
                ((caret y) != 0) if: [
                    nextX <- ((lines at: ((caret y) - 1)) text size).
                    nextY <- ((caret y) - 1).
                ]
            ].

            deleteCharAtX: (caret x) Y: (caret y).

            moveCaretToX: nextX Y: nextY.
            
          ^ self
        );

        insertText: text AtX: x Y: y => (
            |
                oldtext <- (lines at: y) text,
                newtext <- (std io StringWriter) new
            |
            (x < oldtext size) if: [
                oldtext forEach: [ :i :c |
                    (i == x) if: [ newtext << text ].
                    newtext << c.
                ].
            ] else: [
                newtext << oldtext << text.
            ].
            replaceLine: y With: newtext build.
          ^ self
        );
        insertText: text => (
            insertText: text AtX: (caret x) Y: (caret y).
            moveCaretToX: ((caret x) + text size) Y: (caret y).
          ^ self
        );

        insertNewlineAtX: x Y: y => (
            |
                first  <- (lines at: y) text substringTo: x,
                second <- (lines at: y) text substringFrom: x
            |
            replaceLine: y With: first.
            insertLineAfter: y With: second.
          ^ self
        );
        insertNewline => (
            insertNewlineAtX: (caret x) Y: (caret y).
            moveCaretToX: 0 Y: ((caret y) + 1).
          ^ self
        );

        protected readjustCaret => (
            "TODO, FIXME: If the line list is completely empty, this will fail."
            |
                y <- (((caret y) min: (lines size - 1)) max: 0),
                x <- (((caret x) min: ((lines at: (y)) text size)) max: 0)
            |
            caret <- morphic X: x Y: y.
          ^ self
        );
        moveCaretToX: x Y: y => (
            caret <- morphic X: x Y: y.
            readjustCaret.
            renderDirty.
          ^ self
        );
        moveCaretTo: point => moveCaretToX: (point x) Y: (point y);

        protected caretToPixel(caret) => (
            |
                caretX <- (lines at: (caret y)) caretX: (caret x),
                caretY <- 0,
                y      <- 0
            |
            lines do: [ :l |
                (y < caret y) if: [ caretY <- caretY + l height ].
                y <- y + 1.
            ].
          ^ morphic X: caretX Y: caretY
        );

        protected pixelToCaret(pixel) => (
            |
                x  <- 0,
                y  <- 0,
                dy <- 0,
                dc <- 0
            |
            lines do: [ :l |
                (dy < pixel y) if: [ y <- dc ].
                dy <- dy + l height.
                dc <- dc + 1
            ].

            x <- (lines at: y) charAtPoint: pixel x.
          ^ morphic X: x Y: y
        );

        renderImplOn: renderer => (
            | y <- 0 |
            lines do: [ :l |
                l renderOn: renderer X: 0 Y: y.
                y <- y + l height.
            ].

            | pixelCaret <- caretToPixel(caret) |
            
            renderer setR: 255 G: 0 B: 0.
            renderer drawLineX: (pixelCaret x) Y: (pixelCaret y) X: (pixelCaret x) Y: (pixelCaret y) + (lines at: (caret y)) height.

            $(Morph) renderImplOn: renderer.
        );

        handleKey: key At: point HandMorph: hand => (
            key ifLeft:  [ moveCaretToX: ((caret x) - 1) Y: (caret y). ^ true ].
            key ifRight: [ moveCaretToX: ((caret x) + 1) Y: (caret y). ^ true ].
            key ifUp:    [ moveCaretToX: (caret x) Y: ((caret y) - 1). ^ true ].
            key ifDown:  [ moveCaretToX: (caret x) Y: ((caret y) + 1). ^ true ].

            key ifBackspace: [ deleteChar.                   ^ true ].
            key ifEnter:     [ insertNewline.                ^ true ].
            key ifHome:      [ moveCaretToX: 0 Y: (caret y). ^ true ].
            key ifEnd:       [ moveCaretToX: ((lines at: (caret y)) text size) Y: (caret y). ^ true ].
            key ifTab:       [ insertText: ' ' * (4 - ((caret x) mod: 4)).                   ^ true ].

            key keyText yes: [ :text |
                insertText: text.
              ^ true
            ].

          ^ false
        );
        handleClick: click At: point HandMorph: hand => (
            moveCaretTo: (pixelToCaret(point)).
          ^ true
        );

        fullText => (
            |
                text <- (std io StringWriter) new
            |
            lines do: [ :l | text << l text << '\n' ].
          ^ text build
        );

        clear => (
            [ lines size > 1 ] while: [ removeLine: 0 ].
            replaceLine: 0 With: ''.
            moveCaretToX: 0 Y: 0.
          ^ self
        );

        writeOn: w => w << 'a text editor';
    };
}
