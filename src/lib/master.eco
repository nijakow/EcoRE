"
    master.eco - The Master Object
"

#=lobby: {
    meta = {
        name    => 'Ecosphere Standard Library',
        version => '0.1',
        author  => 'nijakow'
    };

    with packages = {
        with std = #<:std>;
        with interfaces = #<:interfaces>;
        lang = #<:lang>;
    };

    writeOn: writer => writer << 'lobby';
},

#=bootstrapper: {

    InitTypes => (
        #'ecosphere.ecore.init.set_lobby'(#<:lobby>).
        #'ecosphere.ecore.init.set_true'(#<:std/bool/true>).
        #'ecosphere.ecore.init.set_false'(#<:std/bool/false>).
        any <- #'ecosphere.ecore.constant.any'().
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.array_type'(), #<:std/reflect/proxies/Array>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.blob_type'(), #<:std/reflect/proxies/Blob>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.block_type'(), #<:std/reflect/proxies/Block>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.code_type'(), #<:std/reflect/proxies/Code>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.integer_type'(), #<:std/reflect/proxies/Int>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.character_type'(), #<:std/reflect/proxies/Char>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.key_type'(), #<:std/reflect/proxies/Key>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.string_type'(), #<:std/reflect/proxies/String>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.interface_type'(), #<:std/reflect/proxies/Interface>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.ffitype_type'(), #<:std/reflect/proxies/ffi/FFIType>).
        #'ecosphere.ecore.init.set_proxy'(#'ecosphere.ecore.constant.ffifunc_type'(), #<:std/reflect/proxies/ffi/FFIFunc>).
    );

    InitIO => (
        std io in  <- ((std io PortInput) clone) init: ((std io FDByteReader) clone initWithFileDescriptor: (((std io FileDescriptor) clone) init: 0));
        std io out <- ((std io PortOutput) clone) init: ((std io FDByteWriter) clone initWithFileDescriptor: (((std io FileDescriptor) clone) init: 1));
    );

    Quote => (
        io out newline.
        io out << '      Grant me wings that I might fly'. io out newline.
        io out << '              - VNV Nation, "Beloved"'. io out newline.
        io out newline.
    );

    Init => (
        InitTypes.
        InitIO.

        Quote.

        io out << 'EcoRE image online and ready.'.
        io out newline.
        io out commit.

        |
            reader <- (io in) lens: (lang eco tokenizer PortReader),
            tok    <- reader lens: (lang eco tokenizer Tokenizer),
            parser <- tok lens: (lang eco parser Parser)
        |

        parser exprDo: [ :expr |
            |
                env  <- {
                    addVar: name => #'ecosphere.ecore.reflect.add_value_slot'(self, -1, 0, any, name, false);
                    writeOn: writer => writer << '<session>';
                    with inherited parent = #<:std/Clonable>;
                },
                result
            |

            [
                | code <- expr compileInEnv: lobby |
                result <- (code execute(env)).
                io out << '        ' << result.
                io out newline
            ] catch: [
                io out << '--== Error Caught ==--'.
                io out newline.
            ].
            io out commit.
        ]
    );

    with lobby = #<:lobby>;
},

#<:bootstrapper> Init.
