
#=std/os/files: {
    static with inherited _ = #<:std/Module>;

    ListFiles: path => #'ecosphere.ecore.io.list_files'(path);

    here => (std os env Lookup: 'PWD')
                yes: [ :p | Path: p ]
                 no: [ ^ root ];

    Path: fullpath => (
        | path <- root |
        (fullpath splitOnChar: #\/) do: [ :sub | path <- path / sub ].
      ^ path
    );

    Find: name In: path => (
        (path name equals: name) if: [ ^ Yes: path ].

        (path children) do: [ :child |
            (Find: name In: child)
              ~ ifYes: [ :v | ^ v ]
        ].

      ^ No
    );

    own SharedPathTrait = #=std/os/files/SharedPathTrait: {
        exists => #'ecosphere.ecore.io.file_exists'(path);
        isDirectory => #'ecosphere.ecore.io.file_is_directory'(path);
        / name => (
            (name equals: '') -> [ ^ self ].
            (name equals: '.') -> [ ^ self ].
            (name equals: '..') -> [ ^ parent ].
          ^ (module Path) clone initWithName: name
                                        Path: path + '/' + name
                                      Parent: self.
        );
        children => (
            | c <- std util newVector |
            (module ListFiles: path) do: [ :file |
                ([file equals: '.'] or: [file equals: '..'])
                    unless: [ c add: (self / file) ].
            ].
          ^ c asArray
        );
        extension => (
            | tokens <- name splitOnChar: $'.' |
            (tokens size > 1) if: [ ^ tokens at: (tokens size - 1) ].
          ^ ''
        );
        openForReading => io OpenFileForReading: path;
        import => importIn: lobby;
        importIn: obj => io Import: path Path: parent In: obj;
        writeOn: w => w << 'a path(\'' << path << '\')';
    };

    own root = {
        static with inherited _ = #<:std/Singleton>;
        with inherited sharedTrait = #<std/os/files/SharedPathTrait>;
        private module = #<:std/os/files>;

        parent => self;
        name => '/';
        path => '/';
        / name => (module Path) clone initWithName: name Path: '/' + name Parent: self;
    };

    own Path = {
        static with inherited _ = #<:std/Clonable>;
        with inherited sharedTrait = #<std/os/files/SharedPathTrait>;
        private module = #<:std/os/files>;

        parent,
        name,
        path;

        with inherited trait = {
            initWithName: _name Path: _path Parent: _parent => (
                parent <- _parent.
                name   <- _name.
                path   <- _path.
              ^ self
            );
        };
    },
}
