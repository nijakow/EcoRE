"
    std.eco - The std part of the Ecosphere Standard Library
"

{
    Object = #=std/Object: {
        with trait = {
            init      => ;
            clone     => #'ecosphere.ecore.clone'(self);
            new       => clone init;
            mirror    => ;   "TODO"
            is: other => #'ecosphere.ecore.is'(self, other);
            asBool    => true;
            @ second  => ((std util Pair) new) first: self second: second;
        };
        with lobby = #<:lobby>;
    },

    with bool = {
        true = #=std/bool/true: {
            with trait = {
                asBool           => self;
                not              => false;
                if: t else: f    => t value;
                if: t            => t value;
                unless: f        => false;
                true: t false: f => if: t else: f;
                true: t          => if: t;
                false: f         => unless: f;
            };
            with own parent = #<:std/Object>;
        },

        false = #=std/bool/false: {
            with trait = {
                asBool           => self;
                not              => true;
                if: t else: f    => f value;
                if: t            => self;
                unless: f        => f value;
                true: t false: f => if: t else: f;
                true: t          => if: t;
                false: f         => unless: f;
            };
            with own parent = #<:std/Object>;
        }
    },

    util = #<:@util.eco>;
}
