
#=std/io/FDByteReader: {
    private fd;
    private buffer;
    private readHead;
    private writeHead;
    private closed;

    initWithFileDescriptor: _fd => (
        fd        <- _fd.
        buffer    <- std util newBlob: 1024.
        readHead  <- 0.
        writeHead <- 0.
        closed    <- false.
      ^ self
    );

    private bufferEmpty => readHead >= writeHead;

    private refresh => (
        readHead  <- 0.
        writeHead <- (fd readIntoBlob: buffer).
    );
    private optionalRefresh => bufferEmpty if: [ refresh ];

    hasNext => (
        closed if: [ ^ false ].
        optionalRefresh.
        bufferEmpty if: [ closed <- true. ^ false ].
      ^ true
    );
    readByte => (
        optionalRefresh.
        bufferEmpty if: [
          ^ 0
        ] else: [
            | b <- buffer at: readHead |
            readHead <- readHead + 1.
          ^ b
        ]
    );

    close => fd close;

    with inherited parent = #<:std/Clonable>;
}
