
#=std/maybe: {
    Yes = #=std/maybe/Yes: {
        static with inherited parent = #<:std/Clonable>;

        yes;

        with inherited trait = {
            init: v => yes <- v;
            yes: block => block value(yes);
            no: block => self;
            yes: yblock no: nblock => yblock value(yes);
            no: nblock yes: yblock => yblock value(yes);

            or: other => self;

            isYes => true;
            isNo  => false;

            writeOn: writer => writer << 'Yes: ' << yes;
        };
    };

    Yes: value => (#<:std/maybe/Yes> clone) init: value;

    No = #=std/maybe/No: {
        static with inherited parent = #<:std/Singleton>;
        with inherited trait = {
            yes: block => self;
            no: block => block value;
            yes: yblock no: nblock => nblock value;
            no: nblock yes: yblock => nblock value;
            yes => 'Expected a value, got No' throw;
            or: other => other;

            isYes => false;
            isNo  => true;

            writeOn: writer => writer << 'No';
        };
    };
}
