
#=std/util/Range: {
    start, end;

    with inherited trait = {
        initWithStart: _start End: _end => (
            start <- _start min: _end.
            end   <- _start max: _end.
          ^ self
        );

        range => ((end - start) + 1) max: 0;

        size => range;
        at: i => start + i;

        do: block => start to: end do: block;
        forEach: block => 0 for: range do: [ :i | block value(i, at: i) ];

        writeOn: w => w << start << ' to: ' << end;

        with inherited parent = #<:std/util/StandardListConversions>;
        with inherited parent = #<:std/util/StandardListAccess>;
        with inherited parent = #<:std/util/StandardListSearch>;
    };

    static with inherited parent = #<:std/Clonable>;
}
