
#=std/util/Set: {
    with inherited parent = #<:std/Clonable>;

    private elements;

    with inherited trait = {
        with inherited parent = #<:std/util/StandardListConversions>;

        init => (
            elements <- std util newVector.
          ^ self
        );

        from: sequence => ( sequence do: [ :e | add: e ]. ^ self).

        forEach: block => elements forEach: block;
        do: block => elements do: block;
        iterator => elements iterator;

        isEmpty => elements isEmpty;
        size => elements size;

        contains: e => (elements find: e) yes: [ :i | ^ true ] no: [ ^ false ];
        add: e => (contains: e) unless: [ elements append: e ];
        remove: e => (elements find: e) yes: [ :i | elements remove: i ];
        take => (
            elements isEmpty if: [ invalidIndexError ].
            | v <- elements at: 0 |
            elements remove: 0.
          ^ v
        );
        
        writeOn: w => w << 'a set';
    };
}
