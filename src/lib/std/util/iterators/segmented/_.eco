
#=std/util/iterators/segmented: {

    SegmentListLink = {
        private segmented = #<:std/util/iterators/segmented>,
        private reader,
        private cachedValue;

        with inherited trait = {
            initWithSegmentReader: _reader => (
                reader      <- _reader.
                cachedValue <- No.
              ^ self
            );

            private refresh => (
                | segment <- reader nextSegment |
                segment yes: [ :seg |
                    |
                        cache <- ((segmented SegmentListLink) clone) initWithSegmentReader: reader,
                        node  <- ((segmented SegmentListNode) clone) initWithVector: seg Next: cache
                    |
                    cachedValue <- Yes: (Yes: node).
                  ^ Yes: node
                ] no: [
                    cachedValue <- Yes: No.
                  ^ No
                ]
            );

            value => cachedValue
                        yes: [ :v | v ]
                         no: [ refresh ];
        };

        with inherited parent = #<:std/Clonable>;
    };

    SegmentListNode = {
        private vector,
        private bytes,
        private nextPtr,
        private loader;

        with inherited trait = {
            initWithVector: _vector Next: _next => (
                vector   <- _vector.
                nextPtr  <- _next.
              ^ self
            );

            at: index => vector at: index;
            size => vector size;

            next => nextPtr value;
        };

        with inherited parent = #<std/Clonable>;
    };

    IteratorState = {
        private atEOF,
        private node,
        private index;

        with inherited trait = {
            initWithNode: _node Index: _index => (
                _node yes: [ :n |
                    atEOF <- false.
                    node  <- n.
                    index <- _index.
                ] no: [
                    atEOF <- true.
                ].
              ^ self
            );
            initWithNode: node => initWithNode: node Index: 0;

            copy => clone;

            private ensureValidIndex => (
                atEOF -> [ ^ ].
                (index < node size) unless: [
                    (node next)
                        yes: [ :n | node <- n. index <- 0 ]
                         no: [      atEOF <- true         ]
                ].
            );

            has => (
                ensureValidIndex.
                atEOF -> [ ^ false ].
              ^ index < node size
            );

            get => (
                ensureValidIndex.
                atEOF -> [ ^ 0 ].
              ^ node at: index
            );

            forward => (
                atEOF -> [ ^ self ].
                (index < node size)
                      if: [ index <- index + 1 ]
                    else: [
                        (node next)
                            yes: [ :n | node <- n. index <- 0 ]
                             no: [      atEOF <- true         ]
                    ].
              ^ self
            );
        };

        with inherited parent = #<:std/Clonable>;
    };

    Iterator = {
        private segmented = #<:std/util/iterators/segmented>,
        private state;

        with inherited trait = {
            initOnSegmentReader: reader => (
                |
                    node <- (((segmented SegmentListLink) clone) initWithSegmentReader: reader) value
                |
                state <- ((segmented IteratorState) clone) initWithNode: node.
              ^ self
            );

            forward => state forward;

            has => state has;

            current => state get;

            withReset: block => (
                | old_state <- state copy |
              ^ block value([ state <- old_state ])
            );
        };

        with inherited iteratorTrait = #<:std/util/iterators/IteratorTrait>;
        with inherited parent = #<:std/Clonable>;
    }
}
