
#=std/reflect/proxies/Interface: {
    size => #'ecosphere.ecore.object.interface.get_entry_info'(self, 0, -1);

    parents => #'ecosphere.ecore.object.interface.get_parents'(self);
    nameForSlotIndex: i => #'ecosphere.ecore.object.interface.get_entry_info'(self, i, -2);
    returnTypeForSlotIndex: i => #'ecosphere.ecore.object.interface.get_entry_info'(self, i, -3);
    argCountForSlotIndex: i => #'ecosphere.ecore.object.interface.get_entry_info'(self, i, -4);
    hasVarargsForSlotIndex: i => #'ecosphere.ecore.object.interface.get_entry_info'(self, i, -5) isNotZero;
    argTypeForSlotIndex: i Arg: ai => #'ecosphere.ecore.object.interface.get_entry_info'(self, i, ai);

    addParent: parent => #'ecosphere.ecore.object.interface.add_parent'(self, parent);

    implementsMessage: message => #'ecosphere.ecore.object.interface.implements_message'(self, message);
    implementsInterface: interface => #'ecosphere.ecore.object.interface.implements_interface'(self, interface);

    private writeTypeBlock: type On: writer => (
        (any != type) -> [ writer << '[' << type << ']' ]
    );

    writeOn: writer => (
        (self is: any) if: [ writer << 'any'. ^ self ].
        writer << '%[ '.
        parents do: [ :p | writer << 'with ' << p << '. ' ].
        0 for: size do: [ :i |
            writeTypeBlock: (returnTypeForSlotIndex: i) On: writer.
            writer << (nameForSlotIndex: i) << '('.
            | count <- (argCountForSlotIndex: i) |
            0 for: count do: [ :j |
                (j > 0) if: [ writer << ', ' ].
                writer << '[' << (argTypeForSlotIndex: i Arg: j) << ']'.
            ].
            (hasVarargsForSlotIndex: i) if: [ (count > 0) if: [ writer << ', ' ]. writer << '...' ].
            writer << ')'.
            writer << '; '
        ].
        writer << ']'
    );

    static with inherited parent = #<:std/Clonable>;
}
