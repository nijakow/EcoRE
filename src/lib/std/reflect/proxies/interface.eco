
#=std/reflect/proxies/Interface: {
    size => #'ecosphere.ecore.object.interface.get_entry_info'(self itself, 0, -1);
    nameForSlotIndex: i => #'ecosphere.ecore.object.interface.get_entry_info'(self itself, i, -2);
    returnTypeForSlotIndex: i => #'ecosphere.ecore.object.interface.get_entry_info'(self itself, i, -3);
    argCountForSlotIndex: i => #'ecosphere.ecore.object.interface.get_entry_info'(self itself, i, -4);
    hasVarargsForSlotIndex: i => #'ecosphere.ecore.object.interface.get_entry_info'(self itself, i, -5) isNotZero;
    argTypeForSlotIndex: i Arg: ai => #'ecosphere.ecore.object.interface.get_entry_info'(self itself, i, ai);

    writeOn: writer => (
        (self is: any) if: [ writer << 'any'. ^ self ].
        writer << '%[ '.
        0 for: size do: [ :i |
            writer << '[' << (returnTypeForSlotIndex: i) << '] '
                   << (nameForSlotIndex: i) << '('.
            | count <- (argCountForSlotIndex: i) |
            0 for: count do: [ :j |
                (j > 0) if: [ writer << ', ' ].
                writer << '[' << (argTypeForSlotIndex: i Arg: j) << ']'.
            ].
            (hasVarargsForSlotIndex: i) if: [ (count > 0) if: [ writer << ', ' ]. writer << '...' ].
            writer << ')'.
            writer << '; '
        ].
        writer << ']'
    );

    with inherited parent = #<:std/Clonable>;
}
