#=lang/eco/parser/Parser/Followups: {
    static with inherited _ = #<:std/Module>;

    all = { level => 2, dec => #<:lang/eco/parser/Parser/Followups> binary },
    binary = { level => 1, dec => #<:lang/eco/parser/Parser/Followups> unary },
    unary = { level => 0, dec => self }
},

#=lang/eco/parser/Parser/Expression: {
    static with inherited _ = #<:std/Module>;

    parseExpression => parseExpressionWithFollowups: #<:lang/eco/parser/Parser/Followups> all;
    parseExpressions => parseExpressionsWithTerminator: TokenType RPAREN;

    parseResend => (
        | v <- parseExpression |
        expect: TokenType RPAREN.
      ^ newASTResendWithValue: v
    );

    parseSimpleExpressionWithFollowups: followups => (
        (check: TokenType SELF)       yes: [ ^ newASTSelf     ].
        (check: TokenType MY)         yes: [ ^ newASTMy       ].
        (check: TokenType LPAREN)     yes: [ ^ parseCompound  ].
        (check: TokenType LBRACK)     yes: [ ^ parseBlock     ].
        (check: TokenType LCURLY)     yes: [ ^ parseMolecule  ].
        (check: TokenType HASHLPAREN) yes: [ ^ parseArray     ].
        (check: TokenType PERCLBRACK) yes: [ ^ parseInterface ].
        (check: TokenType DOLLAR)     yes: [ ^ newASTHostRef  ].
        (check: TokenType RESEND)     yes: [ ^ parseResend    ].

        (check: TokenType KEY) yes: [ :kw |
            (check: TokenType LPAREN) yes: [
                parseArglist let: [ :arglist |
                  ^ newASTBuiltinWithName: (kw payload)
                                     Args: (arglist first)
                               HasVarargs: (arglist second)
                ]
            ].
          ^ newASTConstantWithValue: kw payload
        ].

        (check: TokenType STRING) yes: [ :kw | ^ newASTConstantWithValue: (kw payload) ].

        (check: TokenType NUMBER) yes: [ :kw | ^ newASTConstantWithValue: (kw payload) ].

        (check: TokenType CHARACTER) yes: [ :kw | ^ newASTConstantWithValue: (kw payload) ].

      ^ newASTImplicit
    );

    parseExpressionWithFollowups: followups => (
        ([ followups level > 1 ] and: [ (check: TokenType CARET) isYes ]) if: [
          ^ newASTReturnWithValue: (parseExpressionWithFollowups: followups)
        ].
        ([ followups level > 1 ] and: [ (check: TokenType BAR) isYes ]) if: [ ^ parseBar ].

        |
            ast <- parseSimpleExpressionWithFollowups: followups,
            next <- false
        |

        [
            next <- ast.
            ast  <- parseSendWithAST: ast Followups: followups.
            begin
              if: [ (check: TokenType LPAREN) isYes ] then: [
                parseArglist let: [
                  :arglist | ast <- newASTCallWithSubject: (ast)
                                                     Args: (arglist first)
                                               HasVarargs: (arglist second)
                ]
              ] ~
              if: [ (check: TokenType AS) isYes ] then: [
                  ast <- newASTAsWithLHS: (ast)
                                     RHS: (parseExpressionWithFollowups: false)
              ] ~
              if: [ (check: TokenType ASSIGNMENT) isYes ] then: [
                  ast <- newASTAssignmentWithLHS: (ast)
                                             RHS: (parseExpressionWithFollowups: followups)
              ] ~
              if: [ [ followups level > 1 ] and: [ (check: TokenType TILDE) isYes ] ] then: [
              ] ~
              if: [ ast is: next ] then: [
                ^ ast
              ] ~
            default: []
        ] repeat.
    );

    parseExpressionsWithTerminator: terminator => (
        | exprs <- std util newVector |
        [ (check: terminator) isYes ] until: [
            exprs append: parseExpression.
            (check: terminator) yes: [ ^ exprs ].
            expect: TokenType SEPARATOR
        ].
      ^ exprs
    );
}
