
#=lang/eco/parser/Parser/Send: {
    static with inherited _ = #<:std/Module>;

    tryParseNArySendWithAST: ast => (
        ( | ident <- checkNAryIdentifier |
            ident yes: [ :v |
                |
                    nameStr <- '',
                    arglist <- std util newVector
                |

                [
                    nameStr <- nameStr + (v payload asString).
                    arglist append: (parseExpressionWithFollowups: #<:lang/eco/parser/Parser/Followups> binary).
                    ident <- checkNAryIdentifier.
                    ident yes: [ :val | v <- val ]
                           no: [
                      ^ newASTSendWithSubject: ast
                                      Message: nameStr asKey
                                         Args: arglist
                                   HasVarargs: false
                    ]
                ] repeat
            ]
        ).
      ^ ast
    );

    parseSendWithAST: ast Followups: followups => (
        ( | ident <- checkUnaryIdentifierOrKey |
            ident yes: [ :v |
                | arglist <- parseOptionalArglist |
              ^ newASTSendWithSubject: ast
                              Message: v payload
                                 Args: arglist first
                           HasVarargs: arglist second
            ]
        ).

        (followups level > 0) unless: [ ^ ast ].

        ( | ident <- checkBinaryIdentifier |
            ident yes: [ :v |
                | args <- std util newVector |
                args append: (parseExpressionWithFollowups: #<:lang/eco/parser/Parser/Followups> unary).
              ^ newASTSendWithSubject: ast
                              Message: v payload
                                 Args: args
                           HasVarargs: false
            ]
        ).

        (followups level > 1) unless: [ ^ ast ].

      ^ tryParseNArySendWithAST: ast
    );
}
