
#=lang/eco/compiler/ScopeSharedInfo: {
    static with inherited parent = #<:std/Clonable>;
    with inherited module = #<:lang/eco/compiler>;

    lexicalParent, compiler, argCount, hasVarargs;

    initWithMaybeLexicalParent: ml => (
        lexicalParent <- ml.
        compiler      <- (Compiler clone) initWithDepth: depth.
        argCount      <- 0.
        hasVarargs    <- false.
      ^ self
    );
    initWithLexicalParent: l => initWithMaybeLexicalParent: (Yes: l);
    init => initWithMaybeLexicalParent: No;

    depth => lexicalParent yes: [ :p | p depth + 1 ] no: [ 0 ];

    addParameter => argCount <- argCount + 1;
    enableVarargs => hasVarargs <- true;
},

#=lang/eco/compiler/VariableInfo: {
    static with inherited parent = #<:std/Clonable>;
    with inherited module = #<:lang/eco/compiler>;

    details, type;

    initWithDetails: _details Type: _type => (
        details <- _details.
        type    <- _type.
      ^ self
    );
},

#=lang/eco/compiler/Scope: {
    static with inherited parent = #<:std/Clonable>;
    with inherited module = #<:lang/eco/compiler>;

    shared, parent_scope, bindings;

    initWithShared: s => (
        shared <- s.
      ^ initWithParent: self
    );

    initWithParent: p => (
        shared <- p shared.
        parent_scope <- p.
        bindings <- std util newMap.
      ^ self
    );

    createSubscope => (Scope clone) initWithParent: self;

    parentScope => parent_scope;
    lexicalParentScope => shared lexicalParent;
    compiler => shared compiler;
    depth => shared depth;


    finish => compiler finishWithArgs: (shared argCount)
                              Varargs: (shared hasVarargs);

    addParameter: varname => (
      shared addParameter.
      bindArg: varname
    );
    enableVarargs => shared enableVarargs;

    bindVariable: varname Type: type => (
      |
          r    <- compiler regAlloc allocate,
          info <- (#<:lang/eco/compiler/VariableInfo> clone) initWithDetails: (ValueType Reg: r) Type: type
      |
      bindings at: varname put: info
    );

    bindVariable: name => bindVariable: name Type: any;

    bindArg: name => (
      | info <- (#<:lang/eco/compiler/VariableInfo> clone) initWithDetails: (ValueType Arg: shared argCount) Type: any |
      bindings at: name put: info
    );

    findVariable: varname IfFound: found IfNotFound: notfound Depth: depth => (
        (bindings at: varname) yes: [ :info | ^ found value(info, depth) ].
        (parentScope is: self) if: [
          (shared lexicalParent) yes: [
             :lex | lex findVariable: varname
                             IfFound: [ :info, :depth | ^ found value(info, depth) ]
                          IfNotFound: [ ^ notfound value ]
                               Depth: (depth + 1)
          ] no: [ ^ notfound value ]
        ].
      ^ parentScope findVariable: varname IfFound: found IfNotFound: notfound Depth: depth
    );

    findVariable: varname IfFound: found IfNotFound: notfound => (
        findVariable: varname IfFound: found IfNotFound: notfound Depth: 0
    );
}
