
#=lang/eco/compiler/CodeWriter: {
    static with inherited parent = #<:std/Clonable>;
    with inherited module = #<:lang/eco/compiler>;

    bytes, constants, maxRegister;

    init => (
        bytes       <- std util newVector.
        constants   <- std util newVector.
        maxRegister <- 0.
      ^ self
    );

    finishWithArgs: args Varargs: varargs => (
        "WARNING: UNCOMMENTING THIS LINE INVOKES AN OBJECT'S #'writeOn:' METHOD, EVEN IF THE OBJECT IS INCOMPLETE!"
        /"
            io out << 'Dumping newly created code object...'. io out newline.
            io out << bytes. io out newline.
            io out << constants. io out newline.
            io out << 'Registers: ' << (maxRegister + 1) << ' Args: ' << args << ' Varargs: ' << varargs. io out newline.
            io out commit.
        "/
        #'ecosphere.ecore.object.code.new'(bytes asBlob,
                                           constants asArray,
                                           maxRegister + 1,
                                           args,
                                           varargs if: 1 else: 0)
    );

    protected introduceRegister: r => maxRegister <- maxRegister max: (r mod: 128);

    writeU8: b => bytes append: b;
    writeU16: s => (
        writeU8: (s bitAnd: 0xff).
        writeU8: ((s rshift: 8) bitAnd: 0xff)
    );
    addConstant: c => (
        writeU16: constants size.
        constants append: c
    );
    addClosure: c => addConstant: c;

    writeNOOP => writeU8: Bytecodes NOOP;

    writeConst: c => (
        writeU8: Bytecodes CONST.
        addConstant: c.
    );

    writePushConst: c => (
        writeConst: c.
        writePush.
    );

    writeClosure: c => (
        writeU8: Bytecodes CLOSURE.
        addClosure: c.
    );

    writePush => (
        writeU8: Bytecodes PUSH.
    );

    writePop => (
        writeU8: Bytecodes POP.
    );

    writeDrop => writePop;

    writeDup => ( writePop. writePush. writePush. );

    writeLoadLocal: r => (
        introduceRegister: r.
        writeU8: Bytecodes LOAD_LOCAL.
        writeU8: r.
    );

    writeStoreLocal: r => (
        introduceRegister: r.
        writeU8: Bytecodes STORE_LOCAL.
        writeU8: r.
    );

    writeLoadLexical: r Depth: d => (
        writeU8: Bytecodes LOAD_LEXICAL.
        writeU8: d.
        writeU8: r.
    );

    writeStoreLexical: r Depth: d => (
        writeU8: Bytecodes STORE_LEXICAL.
        writeU8: d.
        writeU8: r.
    );

    writeLoad: r => writeLoadLocal: r;
    writeStore: r => writeStoreLocal: r;

    writeLoad: r Depth: d => (
        (d isZero)
          ~ if: [ writeLoadLocal: r            ]
          else: [ writeLoadLexical: r Depth: d ].
    );

    writeStore: r Depth: d => (
        (d isZero)
          ~ if: [ writeStoreLocal: r            ]
          else: [ writeStoreLexical: r Depth: d ].
    );

    writeReg: src ToReg: dst => (
        writeLoadLocal: src.
        writeStoreLocal: dst.
    );

    writeReg: src ToReg: dst Depth: depth => (
        writeLoadLexical: src.
        writeStore: dst Depth: depth.
    );

    writeReg: src Depth: depth ToReg: dst => (
        writeLoad: src Depth: depth.
        writeStoreLexical: dst.
    );

    writeReg: src Depth: srcdepth ToReg: dst Depth: dstdepth => (
        writeLoad: src Depth: srcdepth.
        writeStore: dst Depth: dstdepth.
    );

    writeBuiltin: key Args: args Varargs: varargs => (
        (varargs) if: [ writeU8: Bytecodes BUILTINV ]
                else: [ writeU8: Bytecodes BUILTIN  ].
        writeU8: args.
        addConstant: key
    );

    writeBuiltin: key Args: args => writeBuiltin: key Args: args Varargs: false;

    writeSend: message Args: args Varargs: varargs => (
        (varargs) if: [ writeU8: Bytecodes SENDV ]
                else: [ writeU8: Bytecodes SEND  ].
        writeU8: args.
        addConstant: message
    );

    writeSend: message Args: args => writeSend: message Args: args Varargs: false;

    writeResend: message Args: args Varargs: varargs => (
        (varargs) if: [ writeU8: Bytecodes RESENDV ]
                else: [ writeU8: Bytecodes RESEND  ].
        writeU8: args.
        addConstant: message
    );

    writeResend: message Args: args => writeResend: message Args: args Varargs: false;

    writeAssign: key => (
        writeU8: Bytecodes ASSIGN.
        addConstant: key
    );

    writeReturn: depth => (
        writeU8: Bytecodes RETURN.
        writeU8: depth
    );
}
