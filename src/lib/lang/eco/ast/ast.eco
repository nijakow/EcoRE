
#=lang/eco/ast: {
    with construction = #<:lang/eco/ast/ASTConstruction>;
    
    IVisitor = #<:lang/eco/ast/IVisitor>;

    AST_SharedBehavior = #=lang/eco/ast/AST_SharedBehavior: {
        acceptVisitor: visitor => visitor visitUnknown: self;
        writeOn: writer => writer << 'AST';
        compileInEnv: env Host: host => compileInEnv: env;
        compileInEnv: env => (
            |
                compiler <- (lang eco compiler ScopedCompiler) new,
                visitor  <- ((lang eco compiler Visitor) clone) initWithCompiler: compiler Environment: env
            |
            acceptVisitor: visitor.
          ^ compiler finish
        );
        evaluateInEnv: env => (compileInEnv: env) execute(env);
        evaluateInEnv: env Host: host => (compileInEnv: env Host: host) execute(env);
        isSelfAST => false;
    };

    ASTFlags = {
        isDeprecated, isPrivate, isFinal, isWith, isInherited, isOwn, isMethod;

        init => (
            isDeprecated <- false.
            isPrivate    <- false.
            isFinal      <- false.
            isWith       <- false.
            isInherited  <- false.
            isOwn        <- false.
            isMethod     <- false.
          ^ self
        );

        setDeprecated => ( isDeprecated <- true. self );
        setPrivate    => ( isPrivate <- true. self );
        setFinal      => ( isFinal <- true. self );
        setWith       => ( isWith <- true. self );
        setInherited  => ( isInherited <- true. self );
        setOwn        => ( isOwn <- true. self );
        setMethod     => ( isMethod <- true. self );

        writeOn: writer => (
            isDeprecated if: [ writer << '@deprecated ' ].
            isPrivate    if: [ writer << 'private '     ].
            isFinal      if: [ writer << 'final '       ].
            isWith       if: [ writer << 'with '        ].
            isInherited  if: [ writer << 'inherited '   ].
            isOwn        if: [ writer << 'own '         ].
            isMethod     if: [ writer << '<method> '    ]. "isMethod is usually not printed"
        );

        with inherited parent = #<:std/Clonable>;
    };

    ASTTypeAndName = {
        type, name;

        initWithType: t Name: n => (
            type <- t.
            name <- n.
          ^ self
        );

        getType => type;
        getName => name;

        writeOn: writer => writer << '[' << type << '] ' << name;

        with inherited parent = #<:std/Clonable>;
    };

    ASTSlotDef = {
        flags, type, name, args, varargs, body;

        initWithFlags: f Type: t Name: n Args: a Varargs: v Body: b => (
            flags   <- f.
            type    <- t.
            name    <- n.
            args    <- a.
            varargs <- v.
            body    <- b.
          ^ self
        );

        writeOn: writer => (
            writer << flags.
            writer << '[' << type << '] '.
            writer << name << ' ('.
            args forEach: [ :i, :e | (i > 0) if: [ writer << ', ' ]. writer << e ].
            writer << (varargs if: ', ...) ' else: ') ').
            writer << body.
        );
        addToInterface: interface WithEnv: env => (
            | realType <- type evaluateInEnv: env,
              realArgs <- std util newArray: args size |
            args forEach: [ :i, :v | realArgs at: i put: (v type evaluateInEnv: env) ].
          ^ #'ecosphere.ecore.object.interface.add_entry'(interface, realType, name, realArgs, varargs if: 1 else: 0)
        );
        addToMolecule: molecule WithEnv: env => (
            "TODO: Use Mirrors for this!"

            | realType  <- type evaluateInEnv: env,
              flagBits  <- 0 |
            
            flags isWith          if: [ flagBits <- flagBits bitOr: 0b0010 ].
            flags isInherited unless: [ flagBits <- flagBits bitOr: 0b0001 ].
            flags isOwn           if: [ flagBits <- flagBits bitOr: 0b0100 ].
            flags isPrivate       if: [ flagBits <- flagBits bitOr: 0b1000 ].

            flags isMethod if: [
                "TODO, FIXME, XXX: This approach is totally horrendous"
                |
                    compiler <- (lang eco compiler ScopedCompiler) new,
                    visitor  <- ((lang eco compiler Visitor) clone) initWithCompiler: compiler Environment: env
                |
                args do: [ :arg | compiler addParameter: arg name "TODO: Also compile and add the type" ].
                varargs if: [ compiler enableVarargs ].
                body acceptVisitor: visitor.
                #'ecosphere.ecore.reflect.add_code_slot'(molecule, -1, flagBits, realType, name, compiler finish).
            ] else: [
                | realValue <- body evaluateInEnv: env |
                #'ecosphere.ecore.reflect.add_value_slot'(molecule, -1, flagBits, realType, name, realValue).
            ].
          ^ molecule
        );
        with inherited parent = #<:std/Clonable>;
    };

    ASTSelf = {
        new => self;
        isSelfAST => true;
        acceptVisitor: visitor => visitor visitSelf: self;
        writeOn: writer => writer << 'self';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTHostRef = {
        new => self;
        acceptVisitor: visitor => visitor visitHostRef: self;
        writeOn: writer => writer << '$';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTConstant = {
        constantValue;

        initWithValue: v => (
            constantValue <- v.
          ^ self
        );
        acceptVisitor: visitor => visitor visitConstant: self;
        writeOn: writer => writer << 'ASTConstant(' << constantValue << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTArray = {
        exprs;

        initWithExprs: _exprs => (
            exprs <- _exprs.
          ^ self
        );
        acceptVisitor: visitor => visitor visitArray: self;
        writeOn: writer => writer << 'ASTArray(' << exprs << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTInterface = {
        parents, entries;

        initWithParents: _parents Entries: _entries => (
            parents <- _parents.
            entries <- _entries.
          ^ self
        );

        acceptVisitor: visitor => visitor visitInterface: self;
        writeOn: writer => writer << 'ASTInterface(' << parents << ', ' << entries << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTMolecule = {
        slots;

        initWithSlots: _slots => (
            slots <- _slots.
          ^ self
        );

        acceptVisitor: visitor => visitor visitMolecule: self;
        writeOn: writer => writer << 'ASTMolecule(' << slots << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTBuiltin = {
        name, args, varargs;

        initWithName: _name Args: _args Varargs: _varargs => (
            name    <- _name.
            args    <- _args.
            varargs <- _varargs.
          ^ self
        );
        getName => name;
        getArgs => args asArray;
        getVarargs => varargs;
        acceptVisitor: visitor => visitor visitBuiltin: self;
        writeOn: writer => writer << 'ASTBuiltin(' << name << ', ' << args << ', ' << varargs << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTCall = {
        subject, args, varargs;

        initWithSubject: _subject Args: _args Varargs: _varargs => (
            subject <- _subject.
            args    <- _args.
            varargs <- _varargs.
          ^ self
        );
        getSubject => subject;
        getArgs => args asArray;
        getVarargs => varargs;
        acceptVisitor: visitor => visitor visitCall: self;
        writeOn: writer => writer << 'ASTCall(' << subject << ', ' << args << ', ' << varargs << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTSend = {
        subject, message, args, varargs;

        initWithSubject: _subject Message: _message Args: _args Varargs: _varargs => (
            subject <- _subject.
            message <- _message.
            args    <- _args.
            varargs <- _varargs.
          ^ self
        );
        getSubject => subject;
        getMessage => message;
        getArgs => args asArray;
        getVarargs => varargs;
        acceptVisitor: visitor => visitor visitSend: self;
        writeOn: writer => writer << 'ASTSend(' << subject << ', ' << message << ', ' << args << ', ' << varargs << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTAssignment = {
        lhs, rhs;

        initWithLHS: _lhs RHS: _rhs => (
            lhs <- _lhs.
            rhs <- _rhs.
          ^ self
        );
        acceptVisitor: visitor => visitor visitAssignment: self;
        writeOn: writer => writer << 'ASTAssignment(' << lhs << ', ' << rhs << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTVariableDeclaration = {
        type, name, value, followup;

        initWithType: _type Name: _name Value: _value Followup: _followup => (
            type     <- _type.
            name     <- _name.
            value    <- _value.
            followup <- _followup.
          ^ self
        );
        getType => type;
        getName => name;
        getValue => value;
        getFollowup => followup;
        acceptVisitor: visitor => visitor visitVariableDeclaration: self;
        writeOn: writer => writer << 'ASTVariableDeclaration(' << type << ', ' << name << ', ' << value << ', ' << followup << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTCompound = {
        exprs;

        initWithExprs: _exprs => (
            exprs <- _exprs.
          ^ self
        );
        getExprs => exprs asArray;
        acceptVisitor: visitor => visitor visitCompound: self;
        writeOn: writer => writer << 'ASTCompound(' << exprs << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTBlock = {
        args, varargs, body;

        initWithArgs: _args Varargs: _varargs Body: _body => (
            args    <- _args.
            varargs <- _varargs.
            body    <- _body.
          ^ self
        );
        getArgs => args asArray;
        hasVarargs => varargs;
        getBody => body;
        acceptVisitor: visitor => visitor visitBlock: self;
        writeOn: writer => writer << 'ASTBlock(' << args << ', ' << varargs << ', ' << body << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTReturn = {
        returnValue;

        initWithValue: _value => (
            returnValue <- _value.
          ^ self
        );
        acceptVisitor: visitor => visitor visitReturn: self;
        writeOn: writer => writer << 'ASTReturn(' << returnValue << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };

    ASTAs = {
        lhs, rhs;

        initWithLHS: _lhs RHS: _rhs => (
            lhs <- _lhs.
            rhs <- _rhs.
          ^ self
        );
        acceptVisitor: visitor => visitor visitAs: self;
        writeOn: writer => writer << 'ASTAs(' << lhs << ', ' << rhs << ')';
        with inherited ast_shared = #<:lang/eco/ast/AST_SharedBehavior>;
        with inherited parent = #<:std/Clonable>;
    };
}
