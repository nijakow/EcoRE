
#=lang/eco/ast/ASTConstruction/ASTInterfaceBuilder: {
    static with inherited parent = #<:std/Clonable>;

    parents, entries;

    with inherited trait = {
        ast = #<:lang/eco/ast>;

        init => (
            parents <- std util newVector.
            entries <- std util newVector.
          ^ self
        );

        addParent: ast => parents append: ast;
        addEntryWithType: type Name: name Args: args Varargs: varargs => (
            entries append: ((((ast ASTSlotDef) new) initWithFlags: ((ast ASTFlags) new) Type: type Name: name Args: args Varargs: varargs Body: ((ast ASTSelf) new)))
        );

        commit => ((ast ASTInterface) new) initWithParents: parents Entries: entries;
    };
},

#=lang/eco/ast/ASTConstruction/ASTMoleculeBuilder: {
    static with inherited parent = #<:std/Clonable>;

    slots;

    with inherited trait = {
        ast = #<:lang/eco/ast>;
        
        init => (
            slots <- std util newVector.
          ^ self
        );

        openFlags => (ast ASTFlags) new;

        addCodeSlotWithFlags: flags Type: type Name: name Args: args Varargs: varargs Body: body => (
            flags setMethod.
            slots append: (((ast ASTSlotDef) new) initWithFlags: flags Type: type Name: name Args: args Varargs: varargs Body: body)
        );

        addValueSlotWithFlags: flags Type: type Name: name Value: value => (
            slots append: ((((ast ASTSlotDef) new) initWithFlags: flags Type: type Name: name Args: (std util newVector) Varargs: false Body: value))
        );

        commit => ((ast ASTMolecule) new) initWithSlots: slots;
    };
},

#=lang/eco/ast/ASTConstruction: {
  ast = #<:lang/eco/ast>;

  newASTType: t AndName: n => (ast ASTTypeAndName new) initWithType: t Name: n;

  newASTSelf => (ast ASTSelf new);

  newASTMy => (ast ASTMy new);

  newASTHostRef => (ast ASTHostRef new);

  newASTResendWithValue: value => (ast ASTResend new) initWithValue: value;

  newASTConstantWithValue: value => (ast ASTConstant new) initWithValue: value;

  newASTArrayWithExpressions: exprs => (ast ASTArray new) initWithExprs: exprs;

  newASTInterfaceBuilder => #<:lang/eco/ast/ASTConstruction/ASTInterfaceBuilder> new;

  newASTMoleculeBuilder => #<:lang/eco/ast/ASTConstruction/ASTMoleculeBuilder> new;

  newASTBuiltinWithName: name Args: args HasVarargs: varargs => (ast ASTBuiltin new) initWithName: name Args: args Varargs: varargs;

  newASTCallWithSubject: subject Args: args HasVarargs: varargs => (ast ASTCall new) initWithSubject: subject Args: args Varargs: varargs;

  newASTSendWithSubject: subject Message: msg Args: args HasVarargs: varargs => (ast ASTSend new) initWithSubject: subject Message: msg Args: args Varargs: varargs;

  newASTAssignmentWithLHS: lhs RHS: rhs => (ast ASTAssignment new) initWithLHS: lhs RHS: rhs;

  newASTBarWithType: type Var: var Value: value Followup: followup => (ast ASTVariableDeclaration new) initWithType: type Name: var Value: value Followup: followup;

  newASTCompoundWithInstructions: instructions => (ast ASTCompound new) initWithExprs: instructions;

  newASTBlockWithArgs: args Varargs: varargs Body: body => (ast ASTBlock new) initWithArgs: args Varargs: varargs Body: body;

  newASTReturnWithValue: value => (ast ASTReturn new) initWithValue: value;

  newASTAsWithLHS: lhs RHS: rhs => (ast ASTAs new) initWithLHS: lhs RHS: rhs;
}
