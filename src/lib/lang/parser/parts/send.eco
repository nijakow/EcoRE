
#=lang/parser/Parser/Send: {
    tryParseNArySendWithAST: ast => (
        ( | ident <- checkNAryIdentifier |
            ident if: [
                |
                    nameStr <- '',
                    arglist <- std util newVector
                |

                [
                    nameStr <- nameStr + (ident payload asString).
                    arglist append: (parseExpressionWithFollowups: #<:lang/parser/Parser/Followups> binary).
                    ident <- checkNAryIdentifier.
                    ident unless: [
                      ^ newASTSendWithSubject: ast
                                      Message: nameStr asKey
                                         Args: arglist
                                   HasVarargs: false
                    ]
                ] repeat
            ]
        ).
      ^ ast
    );

    parseSendWithAST: ast Followups: followups => (
        ( | ident <- checkUnaryIdentifier |
            ident if: [
                | arglist <- parseOptionalArglist |
              ^ newASTSendWithSubject: ast
                              Message: ident payload
                                 Args: arglist first
                           HasVarargs: arglist second
            ]
        ).

        (followups level > 0) unless: [ ^ ast ].

        ( | ident <- checkBinaryIdentifier |
            ident if: [
                | args <- std util newVector |
                args append: (parseExpressionWithFollowups: #<:lang/parser/Parser/Followups> unary).
              ^ newASTSendWithSubject: ast
                              Message: ident payload
                                 Args: args
                           HasVarargs: false
            ]
        ).

        (followups level > 1) unless: [ ^ ast ].

      ^ tryParseNArySendWithAST: ast
    );
}
